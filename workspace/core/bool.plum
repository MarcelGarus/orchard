# Bool  
# A type with two instances.

import "..string" String
export
  Bool, not Bool, equals Bool Bool, and Bool Bool, or Bool Bool, xor Bool Bool,
  implies Bool Bool, format Bool

Bool = | true false

not bool: Bool -> Bool =
  bool
  % true -> | false
    false -> | true

== a: Bool b: Bool -> Bool =
  a
  % true -> b
    false -> not b

and a: Bool b: Bool -> Bool =
  a
  % true -> b
    false -> | false

or a: Bool b: Bool -> Bool =
  a
  % true -> | true
    false -> b

xor a: Bool b: Bool -> Bool =
  a
  % true -> not b
    false -> b

implies a: Bool b: Bool -> Bool =
  a
  % true -> b
    false -> | true

format b: Bool -> String =
  b
  % true -> "true"
    false -> "false"

if condition: Bool then: t else: t -> t =
  condition
  % true  -> then
    false -> else

if condition: Bool then: t else: (\ -> t) -> t =
  condition
  % true  -> then
    false -> else.call

# fun hash(hasher: &Hasher, value: Bool) { hasher.hash(if value then 0 else 1) }
