# Integers  
# If you write a number such as 3 in the code, it's an Int.

import
  "builtins"
    Int
    add_ints Int Int
    and_ints Int Int
    compare_ints Int Int
    div_ints Int Int
    mod_ints Int Int
    mul_ints Int Int
    or_ints Int Int
    sub_ints Int Int
    xor_ints Int Int
  "..bool" Bool
  "..ordering"
    Ordering
    is_== Ordering
    is_!= Ordering
    is_< Ordering
    is_<= Ordering
    is_> Ordering
    is_>= Ordering
  "..string" String, + String String
  "..never" crash String
export
  Int, + Int Int, - Int Int, * Int Int, / Int Int, mod Int Int, and Int Int,
  or Int Int, xor Int Int, shift_left Int Int, shift_right Int Int, pow Int Int,
  negate Int, abs Int, compare Int Int, < Int Int, > Int Int,
  round_up_to_multiple_of Int, round_up_to_power_of Int, sqrt Int, log_two Int,
  == Int Int, format Int, min Int Int, max Int Int

# var min_int = 0 - 9223372036854775806
# var max_int = 9223372036854775807

+ a: Int b: Int -> Int = add_ints a b
- a: Int b: Int -> Int = sub_ints a b
* a: Int b: Int -> Int = mul_ints a b
/ a: Int b: Int -> Int = div_ints a b
mod a: Int b: Int -> Int = mod_ints a b
and a: Int b: Int -> Int = and_ints a b
or a: Int b: Int -> Int = or_ints a b
xor a: Int b: Int -> Int = xor_ints a b

compare a: Int b: Int -> Ordering = compare_ints a b

== a: Int b: Int -> Bool = compare a b .is_==
!= a: Int b: Int -> Bool = compare a b .is_!=
<  a: Int b: Int -> Bool = compare a b .is_<
>  a: Int b: Int -> Bool = compare a b .is_>
<= a: Int b: Int -> Bool = compare a b .is_<=
>= a: Int b: Int -> Bool = compare a b .is_>=

is_positive a: Int -> Bool = a .> 0
is_negative a: Int -> Bool = a .< 0
is_non_negative a: Int -> Bool = a .>= 0
is_non_positive a: Int -> Bool = a .<= 0

<< int: Int by: Int -> Int =
  by.== 0
  % true -> int
    false -> int .* 2 .<< (by .- 1)

>> int: Int by: Int -> Int =
  by .== 0
  % true -> int
    false -> int ./ 2 .>> (by .- 1)

pow base: Int exponent: Int -> Int =
  exponent .== 0
  % true -> 1
    false -> base .* (pow base (exponent .- 1))

negate int: Int -> Int = 0 .- int

abs int: Int -> Int =
  compare int 0
  % less -> negate int
    equal -> 0
    greater -> int

round_up_to_multiple_of number: Int factor: Int -> Int =
  number .+ factor .- 1 ./ factor .* factor

round_up_to_power_of number: Int base: Int -> Int =
  round_up_to_power_of_rec 1 number base
round_up_to_power_of_rec power: Int number: Int base: Int -> Int =
  # TODO: do this more efficiently
  is_big_enough =
    compare power number
    % less -> | false
      equal -> | true
      greater -> | true
  is_big_enough
  % true -> power
    false -> round_up_to_power_of_rec (power .* base) number base

sqrt num: Int -> Int =
  # num >= 0 or panic("you can't take the sqrt of a negative number")
  sqrt_rec 0 num
sqrt_rec candidate: Int target: Int -> Int =
  next = candidate .+ 1
  next .* next .compare target
  % less -> sqrt_rec next target
    equal -> next
    greater -> candidate

log2 value: Int -> Int =
  value .== 1
  % true -> 0
    false -> log2 (value ./ 2) .+ 1

# lower_byte int: Int -> Byte = lower_byte int

min a: Int b: Int -> Int =
  a .< b
  % true -> a
    false -> b

max a: Int b: Int -> Int =
  a .> b
  % true -> a
    false -> b

#fun parse_int(string: String): Maybe[Int] {
#  var num = 0
#  for char in string do {
#    if not({#0..=#9}.contains(char))
#    then return none[Int]()
#    num = num * 10 + {char - #0}.to_int()
#  }
#  some(num)
#}

# TODO: support different radixes
format int: Int -> String =
  int.is_negative
  % true -> + "-" (negate int .format)
    false ->
      +
        int .< 10
        % true -> ""
          false -> int ./ 10 .format
        int .mod 10 .format_digit
format_digit digit: Int -> String =
  digit .== 0
  % true -> "0"
    false ->
      digit .== 1
      % true -> "1"
        false ->
          digit .== 2
          % true -> "2"
            false ->
              digit .== 3
              % true -> "3"
                false ->
                  digit .== 4
                  % true -> "4"
                    false ->
                      digit .== 5
                      % true -> "5"
                        false ->
                          digit .== 6
                          % true -> "6"
                            false ->
                              digit .== 7
                              % true -> "7"
                                false ->
                                  digit .== 8
                                  % true -> "8"
                                    false ->
                                      digit .== 9
                                      % true -> "9"
                                        false ->
                                          crash "unreachable"
