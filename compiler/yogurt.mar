| Your Official Graph Using Resolved Types  
|
| Starting from the main function, called functions are compiled to the Yogurt.
| Generic functions are specilized for concrete types. For every cast, we create
| a function that can cast the value.

import plum.mar

struct Yogurt { entry_point: String, funs: Map[String, YogurtFun] }

struct YogurtFun { dag: Dag[YogurtExpr], body: YogurtBody }
struct YogurtBody { params: Slice[Id], children: Slice[Id], returns: Id }
struct YogurtExpr { op: YogurtOp, type: EggType }

enum YogurtOp {
  param,
  byte: Byte,
  int: Int,
  string: String,
  array: Slice[Id],
  struct_: Map[String, Id],
  member: YogurtMember,
  enum_: YogurtEnum,
  switch_: YogurtSwitch,
  lambda: YogurtLambda,
  call: YogurtCall,
  type: EggType,
}
struct YogurtMember { of: Id, name: String }
struct YogurtSwitch { condition: Id, cases: Map[String, YogurtBody] }
struct YogurtEnum { name: String, value: Id }
struct YogurtLambda { function: String, closure: Id }
struct YogurtCall { callee: YogurtCallee, args: Slice[Id] }
enum YogurtCallee { lambda: Id, fun_: String, builtin: YogurtBuiltin }

enum YogurtBuiltin {
  | byte operations
  lower_byte, byte_to_int,
  | int operations
  add_ints, sub_ints, mul_ints, div_ints, mod_ints, and_ints, or_ints, xor_ints,
  compare_ints,
  | box operations
  box, unbox,
  | array operations
  generate_array, array_get, array_set, array_slice, array_len,
  | error stuff
  crash,
}

fun yogurt(entry_point: String): Yogurt {
  Yogurt { entry_point, funs = map[String, YogurtFun]() }
}

fun signature(module: Module, name: String, params: Slice[EggType]): String {
  var b = string_builder().&
  b."{module}.{name}"
  for param in params do b." {param}"
  b.to_string()
}

fun return_type(fun_: YogurtFun): EggType {
  fun_.dag.type_of(fun_.body.returns)
}

fun write[W](writer: W, yogurt: Yogurt) {
  var first = true
  for fun_ in yogurt.funs do {
    if first then first = false else writer."\n"
    writer."{fun_.key}:"
    writer.write(fun_.value.body, fun_.value.dag, 1)
  }
}
fun write[W](writer: W, body: YogurtBody, dag: Dag[YogurtExpr], indentation: Int) {
  for param in body.params do writer." {param}"
  writer."\n"
  for id in body.children do {
    var expr = dag.get(id)
    for i in 0..indentation do writer."  "
    writer."{id}: {expr.type} = "
    writer.write(expr.op, dag, indentation)
    writer."\n"
  }
  for i in 0..indentation do writer."  "
  writer."{body.returns}"
}
fun write[W](writer: W, op: YogurtOp, dag: Dag[YogurtExpr], indentation: Int) {
  switch op
  case param(param) writer."param {param}"
  case byte(byte) writer."byte {byte}"
  case int(int) writer."int {int}"
  case string(string) writer."string {string.debug()}"
  case array(items) {
    writer."array"
    for item in items do writer." {item}"
  }
  case struct_(struct_) {
    writer."&"
    for field in struct_ do writer." {field.key}: {field.value}"
  }
  case member(member) writer."{member.of}.{member.name}"
  case enum_(enum_) writer."enum {enum_.name} {enum_.value}"
  case switch_(switch_) {
    writer."switch {switch_.condition}"
    for case_ in switch_.cases do {
      writer."\n"
      for i in 0..indentation do writer."  "
      writer."  {case_.key}:"
      writer.write(case_.value, dag, indentation + 2)
    }
  }
  case lambda(lambda)
    writer."lambda [closure {lambda.closure}] {lambda.function}"
  case call(call) {
    writer."call {call.callee}"
    for arg in call.args do writer." {arg}"
  }
  case type(type) writer."type {type}"
}
fun write[W](writer: W, callee: YogurtCallee) {
  switch callee
  case lambda(lambda) writer."{lambda}"
  case fun_(signature) writer."{signature.debug()}"
  case builtin(builtin) writer."{builtin.debug()}"
}
