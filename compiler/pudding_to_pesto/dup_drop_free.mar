import mod.mar

| Creates a function that accepts a value of the given type, traverses that
| value, and calls dup on all inner members. If it reaches a pointer, it instead
| just increments that pointer's reference count.
fun compile_dup_fun(type: YogurtType, pesto: &Pesto): String {
  var signature = "dup {type}"
  if pesto.funs.contains(signature) then return signature
  pesto.funs.&.put(signature, uninitialized[PestoFun]().*)

  var dag = dag[PestoExpr]()
  var builder = body_builder(dag.&, type.to_pesto()).&
  var value = builder.param

  switch type
  case byte {}
  case int {}
  case type {}
  case array {
    var buffer = builder.member(value, "buffer")
    builder.dup_buffer(buffer, pesto).ignore()
  }
  case never {}
  case struct_
    for field in type.assert_no_closure().extend_one_level().struct_.unwrap()
    do
      builder.dup(
        builder.member(value, field.key), field.value.compile(), pesto
      )
  case enum_ {
    var cases = map[String, PestoBody]()
    for variant in type.assert_no_closure().extend_one_level().enum_.unwrap()
    do {
      var payload_type = builder.type_of(value).enum_.unwrap().get(variant.key)
      var builder = body_builder(dag.&, payload_type).&
      if payload_type is ptr then
        builder.increment_reference_count(builder.param)
      else
        builder.dup(builder.param, variant.value.compile(), pesto)
      cases.&.put(variant.key, builder.finish_with_nothing())
    }
    builder.switch_(value, cases).ignore()
  }
  case closure builder.increment_reference_count(value).ignore()
  case lambda
    builder.increment_reference_count(builder.member(value, "closure")).ignore()
  case recursive unreachable()

  var body = builder.finish_with_nothing()
  var fun_ = PestoFun { dag, body }

  pesto.funs.&.put(signature, fun_)
  signature
}

| Creates a function that accepts a value of the given type, traverses that
| value, and calls dup on all inner members. If it reaches a pointer, it instead
| decrements that pointer's reference count. If the reference count reaches
| zero, it drops the pointed-to value and frees the allocation. For lambdas
| where the closure reaches a reference count of zero, it uses the free function
| pointer to free the closure.
fun compile_drop_fun(type: YogurtType, pesto: &Pesto): String {
  var signature = "drop {type}"
  if pesto.funs.contains(signature) then return signature
  pesto.funs.&.put(signature, uninitialized[PestoFun]().*)

  var dag = dag[PestoExpr]()
  var builder = body_builder(dag.&, type.to_pesto()).&
  var value = builder.param

  switch type
  case byte {}
  case int {}
  case type {}
  case array {
    var buffer = builder.member(value, "buffer")
    var item_type =
      type.assert_no_closure().extend_one_level()
        .primitive.unwrap().args.get(0).compile()
    builder.drop_buffer(buffer, item_type, pesto).ignore()
  }
  case never {}
  case struct_
    for field in type.assert_no_closure().extend_one_level().struct_.unwrap()
    do
      builder.drop(
        builder.member(value, field.key), field.value.compile(), pesto
      )
  case enum_ {
    var cases = map[String, PestoBody]()
    for variant in type.assert_no_closure().extend_one_level().enum_.unwrap()
    do {
      var payload_type = variant.value.compile()
      var inline_type = dag.type_of(value).enum_.unwrap().get(variant.key)
      var builder = body_builder(dag.&, inline_type).&
      var payload = builder.param
      if inline_type is ptr then {
        var new_refcount = builder.decrement_reference_count(payload)
        builder.switch_(builder.compare_ints(new_refcount, builder.int(0)), map(
          "less" -> | Never runs in practice; refcount is always >= 0.
            body_builder(dag.&, pesto_nothing.type).&.finish_with_nothing(),
          "greater" ->
            body_builder(dag.&, pesto_nothing.type).&.finish_with_nothing(),
          "equal" -> {
            var builder = body_builder(dag.&, pesto_nothing.type).&
            builder.call(
              payload_type.compile_drop_fun(pesto),
              builder.load(payload, payload_type.to_pesto()),
              pesto_nothing.type,
            )
            builder.free(payload, payload_type.to_pesto())
            builder.finish_with_nothing()
          },
        ))
      } else
        builder.drop(payload, payload_type, pesto)

      cases.&.put(variant.key, builder.finish_with_nothing())
    }
    builder.switch_(value, cases).ignore()
  }
  case closure(captured) {
    var new_refcount = builder.decrement_reference_count(value)
    builder.switch_(builder.compare_ints(new_refcount, builder.int(0)), map(
      "less" -> | Never runs in practice; refcount is always >= 0.
        body_builder(dag.&, pesto_nothing.type).&.finish_with_nothing(),
      "greater" ->
        body_builder(dag.&, pesto_nothing.type).&.finish_with_nothing(),
      "equal" -> {
        var builder = body_builder(dag.&, pesto_nothing.type).&
        var closure = builder.load(value, captured.*.to_pesto())
        builder.call(
          captured.*.compile_drop_fun(pesto), closure, pesto_nothing.type
        )
        builder.free(value, dag.type_of(closure))
        builder.finish_with_nothing()
      },
    )).ignore()
  }
  case lambda {
    var closure_ptr = builder.member(value, "closure")
    var new_refcount = builder.decrement_reference_count(closure_ptr)
    builder.switch_(builder.compare_ints(new_refcount, builder.int(0)), map(
      "less" -> | Never runs in practice; refcount is always >= 0.
        body_builder(dag.&, pesto_nothing.type).&.finish_with_nothing(),
      "greater" ->
        body_builder(dag.&, pesto_nothing.type).&.finish_with_nothing(),
      "equal" -> {
        var builder = body_builder(dag.&, pesto_nothing.type).&
        var free_fun_ptr = builder.member(value, "free")
        builder.call_indirect(free_fun_ptr, closure_ptr, pesto_nothing.type)
        builder.finish_with_nothing()
      },
    )).ignore()
  }
  case recursive unreachable()

  var body = builder.finish_with_nothing()
  var fun_ = PestoFun { dag, body }

  pesto.funs.&.put(signature, fun_)
  signature
}

fun compile_drop_buffer_fun(item_type: YogurtType, pesto: &Pesto): String {
  var signature = "drop buffer({item_type})"
  if pesto.funs.contains(signature) then return signature
  pesto.funs.&.put(signature, uninitialized[PestoFun]().*)

  var dag = dag[PestoExpr]()
  var builder = body_builder(dag.&, PestoType.ptr).&
  var buffer = builder.param

  var new_refcount = builder.decrement_reference_count(buffer)
  builder.switch_(builder.compare_ints(new_refcount, builder.int(0)), map(
    "less" -> | Never runs in practice; refcount is always >= 0.
        body_builder(dag.&, pesto_nothing.type).&.finish_with_nothing(),
    "greater" ->
      body_builder(dag.&, pesto_nothing.type).&.finish_with_nothing(),
    "equal" -> {
      var builder = body_builder(dag.&, pesto_nothing.type).&
      builder.call(
        item_type.compile_drop_buffer_items_fun(pesto),
        builder.struct_(map("buffer" -> buffer, "cursor" -> builder.int(0))),
        pesto_nothing.type,
      )
      builder.free_buffer(buffer, item_type.to_pesto())
      builder.finish_with_nothing()
    },
  )).ignore()

  var body = builder.finish_with_nothing()
  var fun_ = PestoFun { dag, body }

  pesto.funs.&.put(signature, fun_)
  signature
}

| Accepts aggregate with ptr and cursor. Calls itself recursively.
fun compile_drop_buffer_items_fun(item_type: YogurtType, pesto: &Pesto): String {
  var signature = "drop buffer({item_type}) items"
  if pesto.funs.contains(signature) then return signature
  pesto.funs.&.put(signature, uninitialized[PestoFun]().*)

  var dag = dag[PestoExpr]()
  var builder = body_builder(dag.&, PestoType.struct_(map(
    "buffer" -> PestoType.ptr, "cursor" -> PestoType.int
  ))).&
  var buffer = builder.member(builder.param, "buffer")
  var cursor = builder.member(builder.param, "cursor")
  var len = builder.get_buffer_len(buffer)
  var ordering = 
  builder.switch_(builder.compare_ints(cursor, len), map(
    "less" -> {
      var builder = body_builder(dag.&, pesto_nothing.type).&
      var item = builder.load_buffer_item(buffer, item_type.to_pesto(), cursor)
      builder.drop(item, item_type, pesto)
      builder.call(
        signature,
        builder.struct_(map(
          "buffer" -> buffer,
          "cursor" -> builder.add_ints(cursor, builder.int(1)),
        )),
        pesto_nothing.type,
      )
      builder.finish(builder.push(pesto_nothing))
    },
    "greater" -> | Never runs in practice; cursor is always <= len.
      body_builder(dag.&, pesto_nothing.type).&.finish_with_nothing(),
    "equal" ->
      body_builder(dag.&, pesto_nothing.type).&.finish_with_nothing(),
  )).ignore()

  var body = builder.finish_with_nothing()
  var fun_ = PestoFun { dag, body }

  pesto.funs.&.put(signature, fun_)
  signature
}

| Accepts a pointer to a closure that captures the given types. Frees it.
fun compile_closure_free_fun(captured: YogurtType, pesto: &Pesto): String {
  var signature = "free closure of {captured}"
  if pesto.funs.contains(signature) then return signature
  pesto.funs.&.put(signature, uninitialized[PestoFun]().*)

  var dag = dag[PestoExpr]()
  var builder = body_builder(dag.&, PestoType.ptr).&

  var captured_values = builder.load(builder.param, captured.to_pesto())
  builder.drop(captured_values, captured, pesto)
  builder.free(builder.param, captured.to_pesto())

  var body = builder.finish_with_nothing()
  var fun_ = PestoFun { dag, body }

  pesto.funs.&.put(signature, fun_)
  signature
}
