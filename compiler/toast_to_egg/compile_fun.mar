fun compile(
  fun_: ToastFun, module: Module,
  visible_by_module: Map[Module, Set[String]],
  funs: Map[String, UncompiledFun], types: Map[String, EggTypeDef],
): Result[EggFun, Error] {
  var visible = visible()
  var dag = dag[EggExpr]()
  var body = body_builder(dag.&).&

  var param_types = list[EggType]()
  for param in fun_.params do {
    var type = param.type.compile(module, types)?
    var id = body.push_param(type, param.name.src)
    visible.&.define(param.name.string, id)
    param_types.&.push(type)
  }
  var param_types = param_types.to_slice()

  | Push the type variables into the body.
  for var_ in param_types.collect_type_variables() do {
    var id = body.push(
      EggOp.type(type_variable(var_)).expr(type_type, invalid_src)
    )
    visible.&.define(var_, id)
  }
  for param in fun_.params do {
    var type = param.type.compile(module, types)?
    if type == type_type then {
      var id = body.push(
        EggOp.type(type_variable(param.name.string))
          .expr(type_type, invalid_src)
      )
      visible.&.define(param.name.string, id)
    }
  }

  var returned = fun_.body.compile(body, module, visible.&, EggContext {
    visible_by_module, funs, types
  })?

  var return_type = fun_.return_type.compile(module, types)?
  dag.type_of(returned).is_assignable_to(return_type) or
    return error[EggFun, Error](error(
      "Function returns something different than its claimed return type:\n
      'Claimed: {return_type}\n
      'Actual:  {dag.type_of(returned)}",
      fun_.name.src,
    ))
  var returned = body.push_cast(returned, return_type, fun_.name.src)

  var body = body.finish(returned)

  ok[EggFun, Error](EggFun { module, name = fun_.name, dag, body })
}

| Body Builder  

struct EggBodyBuilder {
  dag: &Dag[EggExpr], params: List[Id], children: List[Id]
}
fun body_builder(dag: &Dag[EggExpr]): _ {
  EggBodyBuilder { dag, params = list[Id](), children = list[Id]() }
}
fun finish(body: EggBodyBuilder, id: Id): EggBody {
  EggBody {
    params = body.params.to_slice(),
    children = body.children.to_slice(),
    returns = id,
  }
}
fun push_param(body: &EggBodyBuilder, type: EggType, src: Src): Id {
  var id = body.dag.&.put(EggOp.param.expr(type, src))
  body.params.&.push(id)
  id
}
fun push(body: &EggBodyBuilder, expr: EggExpr): Id {
  var id = body.dag.&.put(expr)
  body.children.&.push(id)
  id
}
fun get(body: EggBodyBuilder, id: Id): EggExpr { body.dag.get(id) }
fun type_of(body: EggBodyBuilder, id: Id): EggType { body.dag.type_of(id) }
fun src_of(body: EggBodyBuilder, id: Id): Src { body.get(id).src }

| Compiling Functions  

struct EggContext {
  | stack: &EggStack,
  visible_by_module: Map[Module, Set[String]],
  funs: Map[String, UncompiledFun],
  types: Map[String, EggTypeDef],
}

fun compile(
  toast: ToastExpr, body: &EggBodyBuilder, module: Module, visible: &Visible,
  context: EggContext,
): Result[Id, Error] {
  ok[Id, Error](
    switch toast
    case todo(src) {
      var message = body.push(EggOp.string("Todo").expr(type_string, src))
      body.push(
        EggOp.call(EggCall {
          callee = EggCallee.builtin(MatchingBuiltin {
            builtin = EggBuiltin.crash,
            substitutions = Substitutions { substitutions = map[String, EggType]() },
          }),
          args = list(message).to_slice(),
        }).expr(type_never, src)
      )
    }
    case int(int) body.push(EggOp.int(int.int).expr(type_int, int.src))
    case string(string)
      body.push(EggOp.string(string.string).expr(type_string, string.src))
    case type(type)
      body.push(
        EggOp.type(type.compile(module, context.types)?)
          .expr(type_type, invalid_src)
      )
    case struct_(struct_) {
      var fields = map[String, Id]()
      var types = map[String, EggType]()
      for field in struct_.fields do {
        var name = field.name.string
        var value = field.value.compile(body, module, visible, context)?
        var type = body.type_of(value)
        if fields.contains(name) then
          return error[Id, Error](error(
            "{name} is in the struct multiple times.",
            struct_.ampersand.src,
          ))
        fields.&.put(name, value)
        types.&.put(name, type)
      }

      body.push(
        EggOp.struct_(fields).expr(type_struct(types), struct_.ampersand.src)
      )
    }
    case enum_(enum_) {
      var name = enum_.name.string
      var value = enum_.value.compile(body, module, visible, context)?
      body.push(
        EggOp.enum_(EggEnum { name, value })
          .expr(type_enum(map(name -> body.type_of(value))), enum_.pipe.src)
      )
    }
    case switch_(switch_) {
      | Compile the condition.
      var condition = switch_.condition.compile(body, module, visible, context)?
      var condition_type = body.type_of(condition)
      var variant_types = condition_type.kind().enum_
        or return error[Id, Error](error(
          "Switched on non-enum: {condition_type}", switch_.percent.src
        ))

      | Make sure that no variant is handled twice, that all variants are
      | handled, and that no extra variants are handled.
      var handled_variants = set[String]()
      for case_ in switch_.cases do {
        if handled_variants.contains(case_.symbol.string) then
          return error[Id, Error](error(
            "{case_.symbol.string} is handled multiple times.",
            switch_.percent.src,
          ))
        handled_variants.&.put(case_.symbol.string)
      }
      for variant in variant_types do
        if not(handled_variants.contains(variant.key)) then
          return error[Id, Error](error(
            "{variant.key} is not handled.", switch_.percent.src
          ))
      for variant in handled_variants do
        if not(variant_types.contains(variant)) then
          return error[Id, Error](error(
            "Handling non-existent variant {variant}.", switch_.percent.src
          ))

      | Compile the individual cases.
      var case_compilers = map[String, Tuple2[EggBodyBuilder, Id]]()
      var case_types = list[EggType]()
      for case_ in switch_.cases do {
        var symbol = case_.symbol.string
        var snapshot = visible.snapshot()
        var case_body = body_builder(body.dag).&
        var payload_type = variant_types.get(symbol)
        if case_.payload is some(pattern) then {
          var payload = case_body.push_param(payload_type, pattern.src())
          pattern.compile_pattern(payload, case_body, module, visible, context)?
        } else
          case_body.push_param(payload_type, switch_.percent.src).ignore()

        var returns = case_.body.compile(case_body, module, visible, context)?
        visible.restore(snapshot)
        case_compilers.&.put(symbol, tuple(case_body.*, returns))
        case_types.&.push(case_body.type_of(returns))
      }
      var common_type = union(case_types.to_slice()) or
        return error[Id, Error](error(
          "Couldn't union types {case_types}", switch_.percent.src
        ))
      var common_type = common_type
      var cases = map[String, EggBody]()
      for compiler in case_compilers do {
        var symbol = compiler.key
        var returns = compiler.value.b
        var compiler = compiler.value.a
        var actual_return = compiler.&.push_cast(
          returns, common_type, switch_.percent.src
        )
        cases.&.put(symbol, compiler.finish(actual_return))
      }

      body.push(
        EggOp.switch_(EggSwitch { condition, cases })
          .expr(common_type, switch_.percent.src)
      )
    }
    case name(name)
      visible.lookup(name.string) or
        return error[Id, Error](error(
          "Unknown referenced name {name.string}.", name.src
        ))
    case lambda(lambda) {
      var snapshot = visible.snapshot()
      var lambda_body = body_builder(body.dag).&
      var param_types = list[EggType]()
      for param in lambda.params do {
        var name = param.name.string
        var type = param.type.compile(module, context.types)?
        var src = param.name.src
        param_types.&.push(type)
        visible.define(name, lambda_body.push_param(type, src))
      }
      var returns = lambda.body.compile(lambda_body, module, visible, context)?
      var return_type = lambda_body.type_of(returns)
      var lambda_body = lambda_body.finish(returns)
      visible.restore(snapshot)

      body.push(
        EggOp.lambda(lambda_body).expr(
          type_lambda(param_types.to_slice(), return_type),
          lambda.backslash.src,
        )
      )
    }
    case call(call) {
      var args = list[Id]()
      for arg in call.args do
        args.&.push(arg.compile(body, module, visible, context)?)
      var args = args.to_slice()

      var arg_types = list[EggType]()
      for arg in args do arg_types.&.push(body.type_of(arg))
      var arg_types = arg_types.to_slice()

      | Find the function name -- either some(String) if it's a function call
      | or none if it's a lambda call.
      var function_name =
        switch call.function.*
        case name(name) {
          | Check if the call is actually a struct access.
          if arg_types.len == 1 then
            if arg_types.get(0).internal_kind() is struct_(fields) then
              if fields.contains(name.string) then {
                | This is a struct access!
                var struct_ = args.get(0)
                var field_type = body.type_of(struct_).kind()
                  .struct_.unwrap().get(name.string)
                return ok[Id, Error](body.push(
                  EggOp.member(EggMember {
                    of = struct_, name = name.string
                  }).expr(field_type, name.src)
                ))
              }

          switch visible.lookup(name.string)
          case some none[StringAndSrc]()
          case none some(name)
        }
        default none[StringAndSrc]()

      switch function_name
      case some(name) { | top-level function
        | Resolve arguments of the type Type.
        var arg_values = list[FunArg]()
        for i in 0..args.len do {
          var type = arg_types.get(i)
          arg_values.&.push(
            if type == type_type then {
              var type_value = body.get(args.get(i)).op.type or
                return error[Id, Error](error(
                  "Arguments of the type Type have to be known at compile-time.",
                  name.src
                ))
              FunArg { value = some(type_value), type }
            } else
              FunArg { value = none[EggType](), type }
          )
        }
        var arg_values = arg_values.to_slice()

        | Look for a matching function.
        var signature_and_substitutions = lookup(
          context.funs, context.visible_by_module.get(module),
          name.string, arg_values, name.src,
        )?
        var signature     = signature_and_substitutions.a
        var substitutions = signature_and_substitutions.b

        var fun_ = context.funs.get(signature)
        var return_type =
          loop
            switch fun_.impl
            case local(local_fun) {
              var return_type = local_fun.fun_.return_type
                .compile(local_fun.module, context.types)?
                .specialize(substitutions)
              break(return_type)
            }
            case imported(new_signature) {
              | TODO: make sure the function is exported
              | TODO: attribute error to import
              | TODO: update substitutions
              signature = new_signature

              for builtin in egg_builtins do
                if signature.starts_with("{builtin_module}.{builtin}") then {
                  var return_type =
                    builtin.signature().return_type
                      .specialize(substitutions)
                  return ok[Id, Error](body.push(
                    EggOp.call(EggCall {
                      callee = EggCallee.builtin(MatchingBuiltin {
                        builtin, substitutions
                      }), args
                    }).expr(return_type, name.src)
                  ))
                }

              fun_ = context.funs.get_maybe(new_signature) or
                return error[Id, Error]({
                  var b = string_builder().&
                  b."Function {new_signature} doesn't exist.\nThese ones exist:"
                  for fun_ in context.funs do b."\n- {fun_.key}"
                  error(b.to_string(), name.src)
                })
            }
            case builtin(builtin) break(builtin.signature().return_type)

        | Cast arguments
        var cast_args = list[Id]()
        for both in zip(args.iter(), fun_.params.iter()) do {
          var arg = both.a
          var expected_type = both.b.type.specialize(substitutions)
          cast_args.&.push(body.push_cast(arg, expected_type, name.src))
        }
        var args = cast_args.to_slice()

        | Call the function.
        body.push(
          EggOp.call(EggCall {
            callee = EggCallee.fun_(MatchingFun { signature, substitutions }),
            args,
          }).expr(return_type, name.src)
        )
      }
      case none { | lambda
        var lambda = call.function.compile(body, module, visible, context)?
        var lambda_type = body.type_of(lambda)
        var lambda_type = lambda_type.kind().lambda or
          return return error[Id, Error](error(
            "Tried to call non-lambda: {lambda_type}",
            call.function.src(),
          ))

        | Cast arguments
        var cast_args = list[Id]()
        for both in zip(args.iter(), lambda_type.args.iter()) do {
          var arg = both.a
          var expected_type = both.b
          body.type_of(arg).is_assignable_to(expected_type) or
            return return error[Id, Error](error(
              "Tried to call lambda with wrong types.",
              call.function.src(),
            ))
          cast_args.&.push(
            body.push_cast(arg, expected_type, call.function.src())
          )
        }
        var args = cast_args.to_slice()

        body.push(
          EggOp.call(EggCall { callee = EggCallee.lambda(lambda), args }).expr(
            lambda_type.return_type, call.function.src()
          )
        )
      }
    }
    case var_(var_) {
      var value = var_.right.compile(body, module, visible, context)?
      var_.left.compile_pattern(value, body, module, visible, context)?
      value
    }
    case body(exprs) {
      var last = none[Id]()
      for expr in exprs do
        last = some(expr.compile(body, module, visible, context)?)
      last or unreachable()
    }
  )
}

fun compile_pattern(
  pattern: ToastExpr, matched: Id,
  body: &EggBodyBuilder, module: Module, visible: &Visible,
  context: EggContext,
): Result[Nothing, Error] {
  switch pattern
  case todo
    error[Nothing, Error](error(
      "Todo in pattern.", pattern.src()
    ))
  case int
    error[Nothing, Error](error(
      "Int literals can't be in patterns.", pattern.src()
    ))
  case string
    error[Nothing, Error](error(
      "String literals can't be in patterns.", pattern.src()
    ))
  case type
    error[Nothing, Error](error(
      "Type literals can't be in patterns.", pattern.src()
    ))
  case struct_(pattern) {
    var type = body.type_of(matched)
    var struct_fields = type.kind().struct_ or
      return error[Nothing, Error](error(
        "Tried to use a struct pattern to match on {type}.",
        pattern.ampersand.src,
      ))

    for field in pattern.fields do
      struct_fields.contains(field.name.string) or
        return error[Nothing, Error](error(
          "Patterns refers to {field.name.string}, but that is not in the 
          'struct {type}.",
          field.name.src,
        ))
    var matched_fields = set[String]()
    for field in pattern.fields do matched_fields.&.put(field.name.string)
    for field in struct_fields do
      matched_fields.contains(field.key) or
        return error[Nothing, Error](error(
          "Pattern does not bind field {field.key} of {type}.",
          pattern.ampersand.src,
        ))

    for field in pattern.fields do {
      var field_type = struct_fields.get(field.name.string)
      var matched_value = body.push(
        EggOp.member(EggMember {
          of = matched, name = field.name.string
        }).expr(field_type, field.name.src)
      )
      field.value.compile_pattern(
        matched_value, body, module, visible, context
      )?
    }

    ok[Nothing, Error]({})
  }
  case enum_(pattern) {
    todo("enum patterns")
    | var type = body.type_of(matched)
    | var enum_variants = type.enum_ or
    |   return error[Nothing, Error](error(
    |     "Tried to use an enum pattern to match on {type}.",
    |     pattern.pipe.src,
    |   ))
    | enum_variants.len == 1 or
    |   return error[Nothing, Error](error(
    |     "Tried to use an enum pattern to match on {type}. You can only match 
    |     'on enums that contain a single variant.",
    |     pattern.pipe.src,
    |   ))
    | pattern.len == 1 or
    |   return error[Nothing, Error](error(
    |     "Tried to use an enum pattern with multiple variants.",
    |     pattern.pipe.src,
    |   ))
    | var matching_variant = pattern.iter().next().unwrap()
    | var matched_variant = enum_variants.iter().next().unwrap()

    | matching_variant.name.string == matched_variant.key

    | var name = enum_.name.string
    | var value = enum_.value.compile(body, module, visible, context)?
    | var type = EggType.enum_(map(name -> body.type_of(value)))
    | body.push(EggOp.enum_(EggEnum { name, value }).expr(type, enum_.pipe.src))
    ok[Nothing, Error]({})
  }
  case switch_ unreachable()
  case name(name) {
    visible.define(name.string, matched)
    ok[Nothing, Error]({})
  }
  case lambda
    error[Nothing, Error](error("Lambdas can't be in patterns.", pattern.src()))
  case call
    error[Nothing, Error](error("Calls can't be in patterns.", pattern.src()))
  case var_
    error[Nothing, Error](error(
      "Variable declarations can't be in patterns.", pattern.src()
    ))
  case body
    error[Nothing, Error](error("Bodies can't be in patterns.", pattern.src()))
}

fun push_cast(body: &EggBodyBuilder, from: Id, to_type: EggType, src: Src): Id {
  var from_type = body.type_of(from)
  if from_type == to_type
  then from
  else body.push(EggOp.cast(from).expr(to_type, src))
}
