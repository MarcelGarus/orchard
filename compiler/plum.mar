| Welcome to the Plum compiler!
|
| Terminology:
| - structs have fields
|   - fields have a name and value/type
| - enums have variants
|   - variants have a name and value/type

import ../../martinaise/stdlib.mar
import module.mar
import error.mar
import formatting.mar
import dag.mar
import type.mar
import builtins.mar
import string_to_bacon.mar
import bacon.mar
import bacon_to_toast.mar
import toast.mar
import toast_to_egg/mod.mar
import egg.mar
import egg_to_yogurt/mod.mar
import yogurt.mar
import yogurt_to_pudding.mar
import pudding.mar
import pudding_to_pesto.mar
import pesto.mar
import pesto_to_waffle.mar
import waffle.mar
import optimize_waffle/mod.mar
import waffle_to_ground.mar
import ground.mar

| Compiler Stages:
| - Source Code
| - Bacon
| - Toast
| - Egg
| - Yogurt
| - Pudding
| - Pesto
| - Waffle
| - Byte Code

fun main(): Never {
  eprintln("Welcome to the Plum compiler!")

  var ground = module("workspace/test/playground").compile() or(error) {
    stderr."Oh no! An error occurred:\n\n{error}\n"
    exit(1)
  }

  var bytes = ground.to_bytes()
  | eprintln(bytes)

  write_file("output.ground", bytes)

  exit(0)
}

fun compile(module: Module): Result[Ground, Error] {
  println("Bacons and Toasts")
  var toasts = map[Module, Toast]()
  module.parse_toasts(toasts.&)?
  | eprintln("Toasts:")
  | for toast in toasts do
  |   eprintln("{toast.key}:\n{toast.value}")
  println("Egg")     var egg     = toasts.to_egg(module)? | eprintln("Egg:\n{egg}")
  println("Yogurt")  var yogurt  = egg.to_yogurt()?       | eprintln("Yogurt:\n{yogurt}")
  println("Pudding") var pudding = yogurt.to_pudding()    | eprintln("Pudding:\n{pudding}")
  println("Pesto")   var pesto   = pudding.to_pesto()     | eprintln("Pesto:\n{pesto}")
  println("Waffle")  var waffle  = pesto.to_waffle()      | eprintln("Waffle:\n{waffle}")
  println("Optimizing") var waffle  = waffle.optimize()   | eprintln("Waffle:\n{waffle}")
  println("Ground")  var ground  = waffle.to_ground()     | eprintln("Ground:\n{ground}")
  ok[Ground, Error](ground)
}
fun parse_toasts(
  module: Module, out: &Map[Module, Toast]
): Result[Nothing, Error] {
  if out.contains(module) or module.is_builtin() then
    return ok[Nothing, Error]({})

  var content = none[String]()
  for path in module.file_paths() do {
    var bytes = read_file(path) or continue
    content = some(bytes.to_string())
    break
  }
  var content = content or
    return error[Nothing, Error](error("Couldn't read file", invalid_src()))

  var bacon = content.to_bacon(module)
  var toast = bacon.to_toast()?
  out.put(module, toast)

  for def in toast.defs do
    if def is import_(import_) then {
      for part in import_.parts do {
        var imported = module.resolve_import(part.module) or(error)
          return error[Nothing, Error](error(error, import_.keyword.src))
        imported.parse_toasts(out)?
      }
    }
  ok[Nothing, Error]({})
}
