| Converting Yogurt to Pudding  

import plum.mar

fun to_pudding(value: YogurtValue, builder: &PuddingBodyBuilder): PuddingExpr {
  switch value.kind
  case int(int) PuddingOp.int(int) $ type_int
  case string(string) PuddingOp.string(string) $ type_string
  case struct_(struct_) {
    var fields = map[String, PuddingId]()
    for field in struct_ do {
      var id = builder.push(field.value.to_pudding(builder))
      fields.&.put(field.key, id)
    }
    PuddingOp.struct_(fields) $ value.type
  }
  case enum_(enum_) {
    var payload = builder.push(enum_.payload.to_pudding(builder))
    PuddingOp.enum_(PuddingEnum { symbol = enum_.symbol, payload }) $ value.type
  }
  case lambda(lambda) {
    var captures = list[PuddingId]()
    for capture in lambda.captures do {
      var id = builder.push(capture.to_pudding(builder))
      captures.&.push(id)
    }
    PuddingOp.lambda(PuddingLambda {
      function = lambda.function, captures = captures.to_slice()
    }) $ value.type
  }
  case type(type) PuddingOp.type(type) $ type_type
}

fun to_pudding(
  expr: YogurtExpr,
  fun_: YogurtFun,
  fun_builder: &PuddingFunBuilder, builder: &PuddingBodyBuilder,
  mapping: &Map[YogurtId, PuddingId],
  yogurt: Yogurt,
): PuddingExpr {
  switch expr.op
  case param unreachable()
  case uninitialized unreachable()
  case comptime(value) value.to_pudding(builder)
  case struct_(struct_) {
    var fields = map[String, PuddingId]()
    for field in struct_ do {
      var id = mapping.get(field.value)
      builder.push(PuddingOp.dup(id) $ PlumType.never)
      fields.&.put(field.key, id)
    }
    PuddingOp.struct_(fields) $ expr.type
  }
  case member(member) {
    var of = mapping.get(member.of)
    builder.push(PuddingOp.dup(of) $ PlumType.never)
    PuddingOp.member(PuddingMember { of, name = member.name }) $ expr.type
  }
  case enum_(enum_) {
    var payload = mapping.get(enum_.payload)
    builder.push(PuddingOp.dup(payload) $ PlumType.never)
    PuddingOp.enum_(PuddingEnum { symbol = enum_.symbol, payload }) $ expr.type
  }
  case switch_(switch_) {
    var condition = mapping.get(switch_.condition)
    builder.push(PuddingOp.dup(condition) $ PlumType.never)
    var cases = map[String, PuddingBody]()
    for case_ in switch_.cases do
      cases.&.put(case_.key, case_.value.to_pudding(
        fun_, fun_builder, mapping, yogurt
      ))
    PuddingOp.switch_(PuddingSwitch { condition, cases }) $ expr.type
  }
  case lambda(lambda) {
    var captures = list[PuddingId]()
    for capture in lambda.captures do {
      var id = mapping.get(capture)
      builder.push(PuddingOp.dup(id) $ PlumType.never)
      captures.&.push(id)
    }
    PuddingOp.lambda(PuddingLambda {
      function = lambda.function, captures = captures.to_slice()
    }) $ expr.type
  }
  case lambda_call(call) {
    var lambda = mapping.get(call.lambda)
    builder.push(PuddingOp.dup(lambda) $ PlumType.never)
    var args = list[PuddingId]()
    for arg in call.args do {
      var id = mapping.get(arg)
      builder.push(PuddingOp.dup(id) $ PlumType.never)
      args.&.push(id)
    }
    PuddingOp.lambda_call(PuddingLambdaCall {
      lambda, args = args.to_slice()
    }) $ expr.type
  }
  case call(call) {
    var args = list[PuddingId]()
    for arg in call.args do {
      var id = mapping.get(arg)
      builder.push(PuddingOp.dup(id) $ PlumType.never)
      args.&.push(id)
    }
    PuddingOp.call(PuddingCall {
      function = call.function, args = args.to_slice(),
    }) $ expr.type
  }
  case instruction(instruction) {
    var args = list[PuddingId]()
    for arg in instruction.args do {
      var id = mapping.get(arg)
      builder.push(PuddingOp.dup(id) $ PlumType.never)
      args.&.push(id)
    }
    PuddingOp.instruction(PuddingInstruction {
      instruction = instruction.instruction, args = args.to_slice()
    }) $ expr.type
  }
}

fun to_pudding(
  body: YogurtBody,
  fun_: YogurtFun, fun_builder: &PuddingFunBuilder,
  mapping: &Map[YogurtId, PuddingId], yogurt: Yogurt,
): PuddingBody {
  var builder = fun_builder.&.body_builder()
  for param in body.params do {
    var id = builder.&.push_param(fun_.get(param).type)
    mapping.&.put(param, id)
  }
  for id in body.exprs do {
    var expr = builder.&.push(fun_.get(id).to_pudding(
      fun_, fun_builder, builder.&, mapping, yogurt
    ))
    mapping.&.put(id, expr)
  }
  var returns = mapping.get(body.returns)

  builder.&.push(PuddingOp.dup(returns) $ PlumType.never)
  for param in body.params do
    builder.&.push(PuddingOp.drop(mapping.get(param)) $ PlumType.never)
  for id in body.exprs do
    builder.&.push(PuddingOp.drop(mapping.get(id)) $ PlumType.never)

  builder.returns(returns)
}

fun to_pudding(fun_: YogurtFun, yogurt: Yogurt, pudding: &Pudding): PuddingFun {
  var fun_builder = pudding_fun_builder()
  var mapping = map[YogurtId, PuddingId]()

  var captures =
    switch fun_.captures
    case none none[Slice[PuddingId]]()
    case some(captures) {
      var mapped = list[PuddingId]()
      for capture in captures do {
        var id = fun_builder.&.create(PuddingOp.param $ fun_.get(capture).type)
        mapping.&.put(capture, id)
        mapped.&.push(id)
      }
      some(mapped.to_slice())
    }

  var body = fun_.body.to_pudding(
    fun_, fun_builder.&, mapping.&, yogurt
  )
  fun_builder.finish(captures, body)
}

fun to_pudding(yogurt: Yogurt): Pudding {
  var pudding = pudding()
  for fun_ in yogurt.funs do
    pudding.funs.&.put(fun_.key, fun_.value.to_pudding(yogurt, pudding.&))
  pudding
}
