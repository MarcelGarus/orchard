import ../plum.mar

fun to_waffle(egg: Egg): Waffle {
  var funs = map[String, WaffleFun]()
  for fun_ in egg.funs do {
    | eprintln("To waffle: {fun_.key}")
    funs.&.put(fun_.key, fun_.value.to_waffle())
  }
  Waffle { entry_point = egg.entry_point, funs }
}

fun to_waffle(fun_: EggFun): WaffleFun {
  var builder = waffle_body_builder().&
  var mapping = map[EggId, WaffleExpr]()

  var param_types = {
    var types = list[EggType]()
    for param in fun_.body.params do types.&.push(param.type())
    types.to_slice()
  }
  var param_layout = param_types.memory_layout_args()
  var param = create(WaffleExpr.param(WaffleParam {
    size = param_layout.size, alignment = param_layout.alignment
  }))
  for p in fun_.body.params.iter().enumerate() do {
    var layout = param_types.get(p.index).memory_layout()
    mapping.&.put(
      p.item,
      waffle_part(
        waffle_local(param),
        param_layout.offsets.get(p.index),
        layout.size,
        layout.alignment,
      ),
    )
  }

  var body = builder.finish(fun_.body.to_waffle(builder, mapping.&))

  WaffleFun { param, body }
}

| Assumes the parameters are already available.
fun to_waffle(
  body: EggBody, builder: &WaffleBodyBuilder, mapping: &Map[EggId, WaffleExpr]
): WaffleExpr {
  for id in body.children do {
    var expr = id.to_waffle(builder, mapping)
    if expr.definitely_diverges() then return expr
    mapping.&.put(id, builder.local(expr))
  }
  mapping.get(body.returns)
}

fun to_waffle(
  id: EggId, builder: &WaffleBodyBuilder, mapping: &Map[EggId, WaffleExpr]
): WaffleExpr {
  switch id.resolve()
  case param unreachable()
  case byte(byte) waffle_byte(byte)
  case lower_byte(int) waffle_lower_byte(mapping.get(int))
  case byte_to_int(byte) waffle_byte_to_int(mapping.get(byte))
  case int(int) waffle_int(int)
  case add(args) waffle_add(mapping.get(args.a), mapping.get(args.b))
  case subtract(args) waffle_subtract(mapping.get(args.a), mapping.get(args.b))
  case multiply(args) waffle_multiply(mapping.get(args.a), mapping.get(args.b))
  case unchecked_divide(args)
    waffle_divide(mapping.get(args.a), mapping.get(args.b))
  case unchecked_modulo(args)
    waffle_modulo(mapping.get(args.a), mapping.get(args.b))
  case shift_left(args)
    waffle_shift_left(mapping.get(args.a), mapping.get(args.b))
  case shift_right(args)
    waffle_shift_right(mapping.get(args.a), mapping.get(args.b))
  case and_(args) waffle_and(mapping.get(args.a), mapping.get(args.b))
  case or_(args)  waffle_or( mapping.get(args.a), mapping.get(args.b))
  case xor(args) waffle_xor(mapping.get(args.a), mapping.get(args.b))
  | Note: The return byte of the compare_ints waffle expression matches the
  | tag for the Ordering enum (| equal greater less).
  case compare_zero(arg) waffle_compare_zero(mapping.get(arg))
  case struct_(fields) {
    if fields.is_not_empty() then
      panic(
        "Only Nothing is allowed when converting to Waffle; other structs 
        'should be layouted."
      )
    waffle_aggregate(empty_slice[WaffleExpr]())
  }
  case switch_on_byte(switch_) {
    var condition = mapping.get(switch_.condition)
    var cases = list[WaffleBody]()
    for case_ in switch_.cases do {
      var builder = waffle_body_builder().&
      cases.&.push(builder.finish(case_.to_waffle(builder, mapping)))
    }
    waffle_switch(condition, cases.to_slice())
  }
  case call_fun(call) {
    var arg_types = list[EggType]()
    for arg in call.args do arg_types.&.push(arg.type())
    var arg_layout = arg_types.to_slice().memory_layout_args()
    var parts = list[WaffleExpr]()
    for part in arg_layout.parts do
      parts.&.push(
        switch part
        case arg(index) mapping.get(call.args.get(index))
        case padding(amount) waffle_padding(amount)
      )
    var args = waffle_aggregate(parts.to_slice())
    var return_layout = id.type().memory_layout()
    waffle_call(call.fun_, args, return_layout.size, return_layout.alignment)
  }
  case function_ptr(signature) waffle_function_ptr(signature)
  case call_indirect(call) {
    var arg_types = list[EggType]()
    for arg in call.args do arg_types.&.push(arg.type())
    var arg_layout = arg_types.to_slice().memory_layout_args()
    var parts = list[WaffleExpr]()
    for part in arg_layout.parts do
      parts.&.push(
        switch part
        case arg(index) mapping.get(call.args.get(index))
        case padding(amount) waffle_padding(amount)
      )
    var args = waffle_aggregate(parts.to_slice())
    var return_layout = id.type().memory_layout()
    waffle_call_indirect(
      mapping.get(call.function_ptr), args,
      return_layout.size, return_layout.alignment,
    )
  }
  | case object_new(value) {
  |   var value = mapping.get(value)
  |   var ptr = builder.local(waffle_malloc(waffle_int(8 + value.size())))
  |   builder.local(waffle_store(ptr,
  |     waffle_aggregate(list(waffle_int(1), value).to_slice())
  |   ))
  |   ptr
  | }
  | case object_load(load) {
  |   var layout = id.type().memory_layout()
  |   builder.local(waffle_load(
  |     waffle_add(mapping.get(load.ptr), waffle_int(8)),
  |     layout.size, layout.alignment
  |   ))
  | }
  | case object_free(free)
  |   builder.local(waffle_free(
  |     mapping.get(free.ptr), waffle_int(8 + free.type.memory_layout().size)
  |   ))
  | case new_buffer(new) {
  |   | On the heap, we allocate space for the data with the following layout:
  |   | [reference count][length][item][item][item]...
  |   var item_layout = new.item_type.memory_layout()
  |   var len = mapping.get(new.length)
  |   var ptr = builder.local(waffle_malloc(
  |     waffle_add(
  |       waffle_int(8 + 8),
  |       waffle_multiply(len, waffle_int(item_layout.stride_size())),
  |     )
  |   ))
  |   builder.local(waffle_store(ptr, waffle_int(1)))
  |   builder.local(waffle_store(waffle_add(ptr, waffle_int(8)), len))
  |   ptr
  | }
  | case get_buffer_length(ptr)
  |   builder.local(
  |     waffle_load(waffle_add(mapping.get(ptr), waffle_int(8)), 8, 8)
  |   )
  | case load_buffer_item(load) {
  |   var item_layout = load.item_type.memory_layout()
  |   builder.local(waffle_load(
  |     waffle_add(
  |       waffle_add(mapping.get(load.buffer), waffle_int(16)),
  |       waffle_multiply(
  |         mapping.get(load.index), waffle_int(item_layout.stride_size())
  |       )
  |     ),
  |     item_layout.size, item_layout.alignment
  |   ))
  | }
  | case store_buffer_item(store) {
  |   var item_layout = store.item_type.memory_layout()
  |   builder.local(waffle_store(
  |     waffle_add(
  |       waffle_add(mapping.get(store.buffer), waffle_int(16)),
  |       waffle_multiply(
  |         mapping.get(store.index), waffle_int(item_layout.stride_size())
  |       )
  |     ),
  |     mapping.get(store.item),
  |   ))
  | }
  | case store_buffer_bytes(store)
  |   builder.local(waffle_store_bytes(
  |     waffle_add(mapping.get(store.buffer), waffle_int(16)), store.bytes
  |   ))
  | case free_buffer(free) {
  |   var item_layout = free.item_type.memory_layout()
  |   var ptr = mapping.get(free.buffer)
  |   var len = builder.local(waffle_load(waffle_add(ptr, waffle_int(8)), 8, 8))
  |   var size = waffle_add(
  |     waffle_int(8 + 8),
  |     waffle_multiply(len, waffle_int(item_layout.stride_size())),
  |   )
  |   builder.local(waffle_free(ptr, size))
  | }
  | case get_refcount(get)
  |   builder.&.local(waffle_load(mapping.get(get.ptr), 8, 8))
  | case set_refcount(set) {
  |   var ptr = mapping.get(set.ptr)
  |   var refcount = mapping.get(set.refcount)
  |   builder.local(waffle_store(ptr, refcount))
  | }
  case crash(crash) {
    | Convert the Plum String to bytes ptr + len.
    |
    | String layout: [buffer][end][start]
    |                 |
    |                 v
    |                [refcount][len][bytes]
    var message = mapping.get(crash.message)
    var ptr_to_buffer = waffle_part(message, 0, 8, 8)
    var ptr_to_bytes = waffle_add(ptr_to_buffer, waffle_int(16))
    var len = waffle_part(message, 8, 8, 8)
    waffle_crash(waffle_aggregate(list(ptr_to_bytes, len).to_slice()))
  }
  case unreachable waffle_unreachable
  case halt waffle_halt
  case loop_(loop_) {
    var var_types = list[EggType]()
    for var_ in loop_.body.params do var_types.&.push(var_.type())
    var var_layout = var_types.to_slice().memory_layout_args()

    var param = create(waffle_param(var_layout.size, var_layout.alignment))

    var parts = list[WaffleExpr]()
    for part in var_layout.parts do
      parts.&.push(
        switch part
        case arg(index) mapping.get(loop_.initial.get(index))
        case padding(amount) waffle_padding(amount)
      )
    var initial = waffle_aggregate(parts.to_slice())

    var body = {
      for var_ in loop_.body.params.iter().enumerate() do {
        var layout = var_.item.type().memory_layout()
        mapping.&.put(
          var_.item,
          waffle_part(
            waffle_local(param),
            var_layout.offsets.get(var_.index),
            layout.size,
            layout.alignment,
          ),
        )
      }
      var builder = waffle_body_builder().&
      builder.finish(loop_.body.to_waffle(builder, mapping))
    }

    waffle_loop(param, initial, body)
  }
  case continue_(continue_) {
    var arg_types = list[EggType]()
    for arg in continue_.next do arg_types.&.push(arg.type())
    var next_layout = arg_types.to_slice().memory_layout_args()
    var parts = list[WaffleExpr]()
    for part in next_layout.parts do
      parts.&.push(
        switch part
        case arg(index) mapping.get(continue_.next.get(index))
        case padding(amount) waffle_padding(amount)
      )
    var next = waffle_aggregate(parts.to_slice())

    var layout = id.type().memory_layout()

    waffle_continue(next, layout.size, layout.alignment)
  }
  case padding(amount) waffle_padding(amount)
  case aggregate(aggregate) {
    var parts = list[WaffleExpr]()
    for part in aggregate do parts.&.push(mapping.get(part))
    waffle_aggregate(parts.to_slice())
  }
  case part(part) waffle_part(mapping.get(part.of), part.offset, part.size, 1)
  case malloc(size) waffle_malloc(mapping.get(size))
  case null_ptr waffle_int(0)
  case free(free) waffle_free(mapping.get(free.ptr), mapping.get(free.size))
  case load_word(load) waffle_load_word(mapping.get(load.ptr))
  case load_byte(load) waffle_load_byte(mapping.get(load.ptr))
  case store_word(store)
    waffle_store_word(mapping.get(store.ptr), mapping.get(store.value))
  case store_byte(store)
    waffle_store_byte(mapping.get(store.ptr), mapping.get(store.value))
  case store_bytes(store)
    waffle_store_bytes(mapping.get(store.ptr), store.bytes)
  default panic({
    var b = string_builder().&
    b."unexpected expression when converting to Waffle:\n"
    b.write(id, namespace().&, no_indentation)
    b.to_string()
  })
}
