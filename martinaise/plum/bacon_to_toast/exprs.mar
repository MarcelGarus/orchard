import exprs.mar

| Type Expressions  
|
| Plum has structural typing. Even though you usually don't think about it that
| way, when you use "generic" types (aka types with type variables) your usage
| really behaves like a function call. For example, `List Int` "calls" the
| definition of List with the argument Int. So, don't be confused if you see
| "type variables" and "type calls" here.
|
| Also, expressions with introducing punctuation ignore the allow whitespace
| setting. They just eagerly continue consuming stuff until they are stopped,
| so you may have to wrap them in parentheses. This allows omitting parentheses
| in some cases. For example, when passing a struct, enum, or lambda to a call,
| you don't have to wrap it in parentheses if it's the last argument.

fun parse_type(
  parser: &LineParser, allow_whitespace: Bool
): Result[Maybe[ToastType], Error] {
  if parser.parse_lambda_type()? is some(type) then return parsed(type)
  if parser.parse_struct_type()? is some(type) then return parsed(type)
  if parser.parse_enum_type()? is some(type) then return parsed(type)
  if parser.parse_parenthesized_type()? is some(expr) then return parsed(expr)
  if allow_whitespace then {
    if parser.parse_type_name() is some(name) then {
      var args = list[ToastType]()
      loop args.&.push(parser.parse_type(false)? or break)
      return parsed(ToastType.call(ToastTypeCall { name, args = args.to_slice() }))
    }
  }

  if parser.parse_type_name() is some(name) then
    return parsed(ToastType.call(ToastTypeCall {
      name, args = empty_slice[ToastType]()
    }))

  if parser.parse_name() is some(name) then
    return parsed(ToastType.variable(name))

  no_match[ToastType]()
}

fun parse_type(bacon: Bacon): Result[ToastType, Error] {
  if bacon.parse_struct_type()? is some(s) then return ok[ToastType, Error](s)
  if bacon.parse_enum_type()?   is some(e) then return ok[ToastType, Error](e)

  var parser = parser(bacon.line, bacon.src)

  if bacon.children.is_empty() then {
    var type = parser.&.parse_type(true)? or
      return error[ToastType, Error](error(
        "Expected a type expression.", parser.src_of_rest()))
    parser.&.ensure_is_at_end()?
    ok[ToastType, Error](type)
  } else {
    var name = parser.&.parse_type_name() or
      return error[ToastType, Error](error(
        "Expected type name.", parser.src_of_rest()))
    parser.&.ensure_is_at_end()?
    var args = bacon.children.parse_types()?
    ok[ToastType, Error](ToastType.call(ToastTypeCall { name, args }))
  }
}

fun parse_types(bacons: Slice[Bacon]): Result[Slice[ToastType], Error] {
  var types = list[ToastType]()
  for bacon in bacons do types.&.push(bacon.parse_type()?)
  ok[Slice[ToastType], Error](types.to_slice())
}

| Value Expressions  
|
| With types handled above, this part parses expressions on the value level.

| Parses an expression that is not a call or member access.
fun parse_expr_atom(
  parser: &LineParser, allow_whitespace: Bool, allow_dots: Bool
): Result[Maybe[ToastExpr], Error] {
  if parser.consume("...")  is some(dots) then return parsed(ToastExpr.todo(dots.src))
  if parser.parse_parenthesized()? is some(expr) then return parsed(expr)
  if parser.parse_int()?    is some(int)    then return parsed(ToastExpr.int(int))
  if parser.parse_string()? is some(string) then return parsed(ToastExpr.string(string))
  if parser.parse_name()    is some(name)   then return parsed(ToastExpr.name(name))
  if parser.parse_struct()? is some(struct_) then return parsed(struct_)
  if parser.parse_enum()?   is some(enum_)   then return parsed(enum_)
  if parser.parse_lambda()? is some(lambda) then return parsed(lambda)
  if parser.parse_type(allow_whitespace)? is some(type) then
    return parsed(ToastExpr.type(type))
  no_match[ToastExpr]()
}

fun parse_expr(
  parser: &LineParser, allow_whitespace: Bool, allow_dots: Bool
): Result[Maybe[ToastExpr], Error] {
  var expr = parser.parse_expr_atom(allow_whitespace, allow_dots)? or
    return no_match[ToastExpr]()

  if allow_whitespace then {
    var args = list[ToastExpr]()
    loop args.&.push(parser.parse_expr(false, false)? or break)
    if args.is_not_empty() then
      expr = ToastExpr.call(ToastCall {
        function = expr.put_on_heap(), args = args.to_slice()
      })
  }

  parser.consume_whitespace()
  if allow_dots then {
    loop {
      if parser.rest().starts_with("...") then break
      parser.consume(".") or break
      var right = parser.parse_expr(allow_whitespace, false)? or
        return bad_input[ToastExpr](error(
          "Expected expression", parser.src_of_rest()))
      expr = dot(expr, right)
    }
  }

  parsed(expr)
}

fun parse_expr(bacon: Bacon): Result[Maybe[ToastExpr], Error] {
  if bacon.parse_string()? is some(string) then
    return parsed(ToastExpr.string(string))
  if bacon.parse_struct()? is some(struct_) then return parsed(struct_)
  if bacon.parse_enum()? is some(enum_) then return parsed(enum_)
  if bacon.parse_lambda()? is some(lambda)  then return parsed(lambda)

  var parser = parser(bacon.line, bacon.src)

  parser.&.consume_whitespace()
  if parser.&.is_done() then return no_match[ToastExpr]()
  var expr = parser.&.parse_expr(true, true)? or
    return bad_input[ToastExpr](error(
      "Expected expression", parser.src_of_rest()))
  var children = bacon.children.parse_exprs()?
  if parser.&.consume("=") is some(equal_sign) then {
    var right =
      if children.is_empty() then {
        var right = parser.&.parse_expr(true, true)? or
          return bad_input[ToastExpr](error(
            "Expected value of variable {expr.debug()}.", parser.src_of_rest()))
        parser.&.ensure_is_at_end()?
        right
      } else
        ToastExpr.body(children)
    expr = ToastExpr.var_(ToastVar {
      left = expr.put_on_heap(), equal_sign, right = right.put_on_heap()
    })
  } else if children.is_not_empty() then
    expr = ToastExpr.call(ToastCall {
      function = expr.put_on_heap(), args = children
    })
  parser.&.ensure_is_at_end()?
  parsed(expr)
}

fun parse_dot(
  bacon: Bacon, previous: ToastExpr
): Result[Maybe[ToastExpr], Error] {
  var parser = parser(bacon.line, bacon.src)
  parser.&.consume_whitespace()
  if parser.rest().starts_with("...") then return no_match[ToastExpr]()
  var dot = parser.&.consume(".") or return no_match[ToastExpr]()

  var right = Bacon {
    line = bacon.line.substr(1..bacon.line.len),
    children = bacon.children,
    src = bacon.src,
  }.parse_expr()? or
      return bad_input[ToastExpr](error(
        "Dot with nothing on the right.", dot.src
      ))

  parsed(dot(previous, right))
}

fun dot(left: ToastExpr, right: ToastExpr): ToastExpr {
  if right is call(call) then {
    var args = list(left)
    for arg in call.args do args.&.push(arg)
    ToastExpr.call(ToastCall {
      function = call.function, args = args.to_slice()
    })
  } else
    ToastExpr.call(ToastCall {
      function = right.put_on_heap(), args = list(left).to_slice()
    })
}

fun parse_exprs(bacons: Slice[Bacon]): Result[Slice[ToastExpr], Error] {
  var exprs = list[ToastExpr]()
  var i = 0
  loop {
    var bacon = bacons.get_maybe(i) or break
    if bacon.parse_backwards_lambda()? is some(backwards) then {
      var body = bacons.without_first(i + 1).parse_exprs()?
      var lambda = ToastExpr.lambda(ToastLambda {
        backslash = backwards.backslash,
        params = backwards.params,
        body = ToastExpr.body(body).put_on_heap(),
      })

      exprs.&.push(
        if backwards.right is call(call) then {
          var args = list[ToastExpr]()
          for arg in call.args do args.&.push(arg)
          args.&.push(lambda)
          ToastExpr.call(ToastCall {
            function = call.function, args = args.to_slice()
          })
        } else
          ToastExpr.call(ToastCall {
            function = backwards.right.put_on_heap(),
            args = list(lambda).to_slice(),
          })
      )
      break
    }
    var expr = bacon.parse_expr()? or { i = i + 1  continue }

    loop {
      var next = bacons.get_maybe(i + 1) or break
      if      next.parse_switch(expr)? is some(new) then { expr = new }
      else if next.parse_dot(expr)?    is some(new) then { expr = new }
      else break
      i = i + 1
    }

    exprs.&.push(expr)
    i = i + 1
  }
  ok[Slice[ToastExpr], Error](exprs.to_slice())
}
