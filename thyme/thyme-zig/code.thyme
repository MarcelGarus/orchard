# Thyme

# Thyme is a functional language that works on a heap of immutable objects. Thyme's operations are
# low-level and unsafe. Similar to assembly, it's dynamically typed and you can cause segfaults if you're
# not careful. This is quite unusual for a functional programming language (I think).
#
# Thyme operates on a heap of immutable objects, or values. Every object contains a couple of words
# (64-bit values). Either, all of the words are literals (just data), or they are pointers to other objects.
# Because the heap tracks this (as well as the size of objects), it knows how objects are connected and
# can deduplicate and garbage collect them.

# Language Features

# Thyme supports number literals, like this:
3
# This just created an object containing a single word on the heap.

# It also supports strings, like this:
"Hello"
# This created an object containing a single word. Of those 8 bytes, 5 are filled with the UTF-8 encoding
# of "Hello", the other 3 are zero. Longer strings will result in bigger objects with multiple words.

# Number and string literals create objects on the heap and the expressions themselves evaluate to a
# pointer to that object. Generally, every expression in Thyme evaluates to a pointer to an object. You can
# use composites (using brackets) to create objects that contain other objects:
[1 "hi"]
# Using a colon, you can index into composites:
[1 "hi"]:0

# You can bind expressions to names and reference them later:
foo = 1
bar = foo

# Now comes the cool part: The entire program itself is part of the heap! This source code you are reading
# is compiled into a linked list of instructions for a stack-based VM. These instructions live on the heap.
# Because there is no separation between data and code (computers are van Neumann-style), we can
# create new instructions on the fly and run them.

# TODO: do that and remove builtins

# Nil
#
# Nil is a value that is used when we don't really need a value. If you have an
# empty body, it implicitly evaluates to nil.

nil = []

# Crashing

crash = |message| @crash(message)
unreachable = || crash("unreachable")
todo = || crash("todo")
assert = |check| if check then nil else crash("assert failed")

# Basic Bool Definitions

true  = 1
false = 0

assert(true)

# Basic Int Operations

+   = |a, b| @add(a, b)
-   = |a, b| @subtract(a, b)
*   = |a, b| @multiply(a, b)
/   = |a, b| @divide(a, b)
mod = |a, b| @modulo(a, b)

== = |a, b| if -(a, b) then false else true
!= = |a, b| if -(a, b) then true else false
<  = |a, b| ==(@compare(a, b), 2)
>  = |a, b| ==(@compare(a, b), 1)
<= = |a, b| !=(@compare(a, b), 1)
>= = |a, b| !=(@compare(a, b), 2)

assert(==(+(1, 1), 2))
assert(==(-(10, 1), 9))
assert(==(*(3, 5), 15))
assert(==(/(8, 2), 4))
assert(10.mod(10).==(0))
assert(12.mod(10).==(2))

min = |a, b| if a.<(b) then a else b
max = |a, b| if a.>(b) then a else b
abs = |a| if a.>=(0) then a else 0.-(a)

is_nil = |obj| ==(@num_words(obj), 0)

# Bool Operations

not = |a| if a then false else true
and = |a, b| if a then b else false
or  = |a, b| if a then true else b
xor = |a, b| if a then not(b) else b

assert(not(false))
assert(not(not(true)))

assert(and(true, true))
assert(not(and(true, false)))
assert(not(and(false, true)))
assert(not(and(false, false)))

assert(or(true, true))
assert(or(true, false))
assert(or(false, true))
assert(not(or(false, false)))

assert(not(xor(true, true)))
assert(xor(true, false))
assert(xor(false, true))
assert(not(xor(false, false)))

# Strings

#get_char = |string, index|
#  bit_and(shift_left(string:(/(index, 8)), (*(mod(index, 8), 8))), 255)

# Linked Lists

cons = |head, tail| [head tail]
head = |list| list:0
tail = |list| list:1
push_rec = |rec, list, item| if is_nil(list) then cons(item, nil) else cons(head(list), rec(rec, tail(list), item))
push = |list, item| push_rec(push_rec, list, item)
get_rec = |rec, list, index| if index.==(0) then list:0 else rec(rec, list:1, index.-(1))
get = |list, index| get_rec(get_rec, list, index)
insert_rec = |rec, list, index, item| {
  if index.==(0) then
    cons(item, list)
  else {
    if list.is_nil() then crash("bad index") else {}
    cons(list:0, rec(rec, list:1, index.-(1), item))
  }
}
insert = |list, index, item| insert_rec(insert_rec, list, index, item)
remove_rec = |rec, list, index| if index.==(0) then list:1 else cons(list:0, rec(rec, list:1, index.-(1)))
remove = |list, index| remove_rec(remove_rec, list, index)
skip_rec = |rec, list, n| if n.==(0) then list else rec(rec, list:1, n.-(1))
skip = |list, n| skip_rec(skip_rec, list, n)
remove_range_rec = |rec, list, start, end| {
  if start.==(0) then list.skip(end) else cons(list:0, rec(rec, list:1, start.-(1), end.-(1)))
}
remove_range = |list, range| remove_range_rec(remove_range_rec, list, range:0, range:1)
replace_range_rec = |rec, list, start, end, item| {
  if start.==(0) then cons(item, list.skip(end)) else cons(list:0, rec(rec, list:1, start.-(1), end.-(1), item))
}
replace_range = |list, range, item| replace_range_rec(replace_range_rec, list, range:0, range:1, item)
concat_rec = |rec, a, b| if is_nil(a) then b else cons(head(a), rec(rec, tail(a), b))
concat = |a, b| concat_rec(concat_rec, a, b)
pop_rec = |rec, list| if list.tail().is_nil() then nil else cons(list.head(), rec(rec, list.tail()))
pop = |list| pop_rec(pop_rec, list)
len_rec = |rec, list| if list.is_nil() then 0 else rec(rec, list.tail()).+(1)
len = |list| len_rec(len_rec, list)

# Turning ints into strings

digit_to_char = |digit| 48.+(digit)
int_to_str_rec = |rec, int|
  if int.>=(10) then
    concat(rec(rec, int./(10)), cons(digit_to_char(int.mod(10)), nil))
  else
    cons(digit_to_char(int), nil)
int_to_str = |int| int_to_str_rec(int_to_str_rec, int)

# Color

rgb = |r, g, b| [r g b]

red = rgb(254, 159, 171)
blue = rgb(1, 175, 218)
yellow = rgb(254, 228, 113)
black = rgb(0, 0, 0)

# Paths

start_path = |point| cons(["move to" point:0 point:1], nil)
line_to = |path, point| push(path, ["line to" point:0 point:1])

# Drawing

fill_path = |path, color| cons(["fill path" path color], nil)
fill_rectangle = |position, size, color| {
  x = position:0
  y = position:1
  w = size:0
  h = size:1
  fill_path(
    start_path([x y])
      .line_to([+(x, w) y])
      .line_to([+(x, w) +(y, h)])
      .line_to([x +(y, h)]),
    color,
  )
}
draw_both = |a, b| cons(["also" a], b)

# Text Rendering

font = {
  _ = 0
  M = 1
  [
    [10 []] # newline
    [" " [[_ _]]]
    ["!" [[_ _] [M M] [M M] [M M] [M M] [M M] [M M] [] [M M] [M M]]]
    [34  [[_ _ _] [M _ M] [M _ M] [M _ M]]] # quotes
    ["#" [[_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ M _ _ M _]
          [M M M M M M]
          [_ M _ _ M _]
          [_ M _ _ M _]
          [_ M _ _ M _]
          [M M M M M M]
          [_ M _ _ M _]]]
    ["$" [[_ _ M _ _]
          [_ M M M _]
          [M _ M _ M]
          [M _ M _ _]
          [M M M _ _]
          [_ M M M _]
          [_ _ M M M]
          [_ _ M _ M]
          [M _ M _ M]
          [_ M M M _]
          [_ _ M _ _]]]
    ["%" [[_ _ _ _ _ _ _ _ _]
          [_ M M _ _ _ M M _]
          [M _ _ M _ _ M M _]
          [M _ _ M _ M M _ _]
          [_ M M _ M M _ _ _]
          [_ _ _ M M M _ _ _]
          [_ _ _ M M _ M M _]
          [_ _ M M _ M _ _ M]
          [_ M M _ _ M _ _ M]
          [_ M M _ _ _ M M _]]]
    ["&" [[_ _ _ _ _ _ _ _]
          [_ M M M M M _ _]
          [M M _ _ M M _ _]
          [M M _ _ M M _ _]
          [_ M M M M _ _ _]
          [_ M M M _ _ M M]
          [M M _ M M _ M M]
          [M M _ _ M M M _]
          [M M _ _ M M M M]
          [_ M M M M _ M M]]]
    ["'" [[_] [M] [M] [M]]]
    ["(" [[_ M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M] [_ M M]]]
    [")" [[M M _] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [M M]]]
    ["*" [[_ _ _ _ _ _] [] [] [M M _ _ M M] [_ M M M M] [_ _ M M] [_ _ M M] [_ M M M M] [M M _ _ M M]]]
    ["+" [[_ _ _ _ _ _] [] [] [_ _ M M] [_ _ M M] [M M M M M M] [M M M M M M] [_ _ M M] [_ _ M M]]]
    ["," [[_ _] [] [] [] [] [] [] [] [M M] [M M] [_ M] [M]]]
    ["-" [[_ _ _ _ _] [] [] [] [] [M M M M M] [M M M M M]]]
    ["." [[_ _] [] [] [] [] [] [] [] [M M] [M M]]]
    ["/" [[_ _ _ M M] [_ _ _ M M] [_ _ M M] [_ _ M M] [_ _ M M] [_ M M] [_ M M] [_ M M] [M M] [M M]]]
    ["0" [[_ _ _ _ _ _]
          [_ M M M M _]
          [M M _ _ M M]
          [M M _ M M M]
          [M M _ M M M]
          [M M M M M M]
          [M M M _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M _]]]
    ["1" [[_ _] [_ M] [M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M]]]
    ["2" [[_ _ _ _ _ _]
          [_ M M M M _]
          [M _ _ _ M M]
          [_ _ _ _ M M]
          [_ _ _ _ M M]
          [_ _ _ M M _]
          [_ _ M M _ _]
          [_ M M _ _ _]
          [M M _ _ _ _]
          [M M M M M M]]]
    ["3" [[_ _ _ _ _ _]
          [M M M M M M]
          [_ _ _ M M _]
          [_ _ M M _ _]
          [_ M M M M _]
          [_ _ _ _ M M]
          [_ _ _ _ M M]
          [_ _ _ _ M M]
          [M _ _ _ M M]
          [_ M M M M _]]]
    ["4" [[_ _ _ _ _ _ _]
          [_ _ _ _ M M _]
          [_ _ _ M M M _]
          [_ _ M _ M M _]
          [_ M _ _ M M _]
          [M M _ _ M M _]
          [M M M M M M M]
          [_ _ _ _ M M _]
          [_ _ _ _ M M _]
          [_ _ _ _ M M _]]]
    ["5" [[_ _ _ _ _ _]
          [M M M M M M]
          [M M _ _ _ _]
          [M M _ _ _ _]
          [M M M M M _]
          [_ _ _ _ M M]
          [_ _ _ _ M M]
          [_ _ _ _ M M]
          [M _ _ _ M M]
          [_ M M M M _]]]
    ["6" [[_ _ _ _ _ _]
          [_ _ M M M _]
          [_ M M _ _ _]
          [M M _ _ _ _]
          [M M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M _]]]
    ["7" [[_ _ _ _ _ _ _]
          [M M M M M M M]
          [_ _ _ _ _ M M]
          [_ _ _ _ _ M M]
          [_ _ _ _ M M _]
          [_ _ _ _ M M _]
          [_ _ _ M M _ _]
          [_ _ _ M M _ _]
          [_ _ _ M M _ _]
          [_ _ _ M M _ _]]]
    ["8" [[_ _ _ _ _ _]
          [_ M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ _ M M _ _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M _]]]
    ["9" [[_ _ _ _ _ _]
          [_ M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M M]
          [_ _ _ _ M M]
          [_ _ _ M M _]
          [_ M M M _ _]]]
    [":" [[_ _] [] [] [M M] [M M] [] [] [] [M M] [M M]]]
    [";" [[_ _] [] [] [M M] [M M] [] [] [] [M M] [M M] [_ M] [M]]]
    ["<" [[_ _ _ _ _] [] [_ _ _ M M] [_ _ M M] [_ M M] [M M] [_ M M] [_ _ M M] [_ _ _ M M]]]
    ["=" [[_ _ _ _ _] [] [] [] [M M M M M] [] [M M M M M]]]
    [">" [[_ _ _ _ _] [] [M M] [_ M M] [_ _ M M] [_ _ _ M M] [_ _ M M] [_ M M] [M M]]]
    ["?" [[_ _ _ _ _ _]
          [_ M M M M _]
          [M _ _ _ M M]
          [_ _ _ _ M M]
          [_ _ _ M M _]
          [_ _ M M _ _]
          [_ _ M M _ _]
          [_ _ _ _ _ _]
          [_ _ M M _ _]
          [_ _ M M _ _]]]
    ["@" [[_ _ _ _ _ _ _ _ _]
          [_ _ M M M M M _ _]
          [_ M _ _ _ _ _ M _]
          [M _ _ M M M _ _ M]
          [M _ M _ _ M _ _ M]
          [M _ M _ _ M _ _ M]
          [M _ M _ _ M _ _ M]
          [M _ _ M M M M M M]
          [_ M _ _ _ _ _ _ _]
          [_ _ M M M M M _ _]]]
    ["A" [[_ _ _ _ _ _]
          [_ M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M M M M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]]]
    ["B" [[_ _ _ _ _ _]
          [M M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M M M _ _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M M M M _]]]
    ["C" [[_ _ _ _ _ _] [_ M M M M] [M M _ _ _ M] [M M] [M M] [M M] [M M] [M M] [M M _ _ _ M] [_ M M M M]]]
    ["D" [[_ _ _ _ _ _]
          [M M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M M M M _]]]
    ["E" [[_ _ _ _ _] [M M M M M] [M M] [M M] [M M] [M M M M] [M M] [M M] [M M] [M M M M M]]]
    ["F" [[_ _ _ _ _] [M M M M M] [M M] [M M] [M M] [M M M M] [M M] [M M] [M M] [M M]]]
    ["G" [[_ _ _ _ _ _]
          [_ M M M M _]
          [M M _ _ _ M]
          [M M _ _ _ _]
          [M M _ _ _ _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M _]]]
    ["H" [[_ _ _ _ _ _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M M M M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]]]
    ["I" [[_ _ _ _] [M M M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [M M M M]]]
    ["J" [[_ _ _ _ _ _]
          [_ _ _ _ M M]
          [_ _ _ _ M M]
          [_ _ _ _ M M]
          [_ _ _ _ M M]
          [_ _ _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M _]]]
    ["K" [[_ _ _ _ _ _ _]
          [M M _ _ _ M M]
          [M M _ _ M M]
          [M M _ M M _]
          [M M M M _ _]
          [M M M _ _ _]
          [M M M M _ _]
          [M M _ M M _]
          [M M _ _ M M]
          [M M _ _ _ M M]]]
    ["L" [[_ _ _ _ _] [M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M M M M]]]
    ["M" [[_ _ _ _ _ _ _ _ _ _]
          [M _ _ _ _ _ _ _ M M]
          [M M _ _ _ _ _ M M M]
          [M M M _ _ _ M M M M]
          [M M M M _ M M M M M]
          [M _ M M M M M _ M M]
          [M _ _ M M M _ _ M M]
          [M _ _ _ M _ _ _ M M]
          [M _ _ _ _ _ _ _ M M]
          [M _ _ _ _ _ _ _ M M]]]
    ["N" [[_ _ _ _ _ _ _]
          [M _ _ _ _ _ M]
          [M M _ _ _ _ M]
          [M M M _ _ _ M]
          [M M M M _ _ M]
          [M _ M M M _ M]
          [M _ _ M M M M]
          [M _ _ _ M M M]
          [M _ _ _ _ M M]
          [M _ _ _ _ _ M]]]
    ["O" [[_ _ _ _ _ _]
          [_ M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M _]]]
    ["P" [[_ _ _ _ _ _]
          [M M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M M M M _]
          [M M _ _ _ _]
          [M M _ _ _ _]
          [M M _ _ _ _]
          [M M _ _ _ _]]]
    ["Q" [[_ _ _ _ _ _ _ _]
          [_ M M M M _ _]
          [M M _ _ M M _]
          [M M _ _ M M _]
          [M M _ _ M M _]
          [M M _ _ M M _]
          [M M _ _ M M _]
          [M M _ _ M M _]
          [M M _ _ M M _]
          [_ M M M M _ _]
          [_ _ _ _ M M _]
          [_ _ _ _ _ M M M]]]
    ["R" [[_ _ _ _ _ _]
          [M M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]]]
    ["S" [[_ _ _ _ _]
          [_ M M M _]
          [M M _ _ M]
          [M M _ _ _]
          [M M M _ _]
          [_ M M M _]
          [_ _ M M M]
          [_ _ _ M M]
          [M _ _ M M]
          [_ M M M _]]]
    ["T" [[_ _ _ _ _ _]
          [M M M M M M]
          [_ _ M M _ _]
          [_ _ M M _ _]
          [_ _ M M _ _]
          [_ _ M M _ _]
          [_ _ M M _ _]
          [_ _ M M _ _]
          [_ _ M M _ _]
          [_ _ M M _ _]]]
    ["U" [[_ _ _ _ _ _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M _]]]
    ["V" [[_ _ _ _ _ _ _]
          [M M _ _ _ M M]
          [M M _ _ _ M M]
          [M M _ _ _ M M]
          [M M _ _ _ M M]
          [_ M M _ M M _]
          [_ M M _ M M _]
          [_ _ M M M _]
          [_ _ M M M _ _]
          [_ _ _ M _ _ _]]]
    ["W" [[_ _ _ _ _ _ _ _ _ _]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M _]
          [M M M M M M M M _ _]]]
    ["X" [[_ _ _ _ _ _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M _]
          [_ _ M M _ _]
          [_ M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]]]
    ["Y" [[_ _ _ _ _ _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M _]
          [_ _ M M _ _]
          [_ _ M M _ _]
          [_ _ M M _ _]
          [_ _ M M _ _]]]
    ["Z" [[_ _ _ _ _ _]
          [M M M M M M]
          [_ _ _ _ M M]
          [_ _ _ M M _]
          [_ _ _ M M _]
          [_ _ M M _ _]
          [_ M M _ _ _]
          [_ M M _ _ _]
          [M M _ _ _ _]
          [M M M M M M]]]
    ["[" [[M M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M M]]]
    ["\\"[[M M _ _ _] [M M] [_ M M] [_ M M] [_ M M] [_ _ M M] [_ _ M M] [_ _ M M] [_ _ _ M M] [_ _ _ M M]]]
    ["]" [[M M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [M M M]]]
    ["^" [[_ _ _ _ _ _] [_ _ M M] [_ M M M M] [M M _ _ M M]]]
    ["_" [[_ _ _ _ _ _] [] [] [] [] [] [] [] [] [M M M M M M]]]
    ["`" [[_ _] [M] [_ M]]]
    ["a" [[_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ M M M M _]
          [M _ _ _ M M]
          [_ _ _ _ M M]
          [_ M M M M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M M]]]
    ["b" [[_ _ _ _ _ _]
          [M M _ _ _ _]
          [M M _ _ _ _]
          [M M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M M M M _]]]
    ["c" [[_ _ _ _ _] [] [] [_ M M M] [M M _ _ M] [M M] [M M] [M M] [M M _ _ M] [_ M M M]]]
    ["d" [[_ _ _ _ _ _]
          [_ _ _ _ M M]
          [_ _ _ _ M M]
          [_ M M M M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M M]]]
    ["e" [[_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M M M M M]
          [M M _ _ _ _]
          [M M _ _ _ M]
          [_ M M M M _]]]
    ["f" [[_ _ _ _ _] [_ _ M M M] [_ M M] [M M M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M]]]
    ["g" [[_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ M M M M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M M]
          [_ _ _ _ M M]
          [M _ _ _ M M]
          [_ M M M M _]]]
    ["h" [[_ _ _ _ _ _]
          [M M _ _ _ _]
          [M M _ _ _ _]
          [M M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]]]
    ["i" [[_ _] [M M] [] [M M] [M M] [M M] [M M] [M M] [M M] [M M]]]
    ["j" [[_ _ _ _ _]
          [_ _ _ M M]
          [_ _ _ _ _]
          [_ _ _ M M]
          [_ _ _ M M]
          [_ _ _ M M]
          [_ _ _ M M]
          [_ _ _ M M]
          [_ _ _ M M]
          [_ _ _ M M]
          [_ _ _ M M]
          [M M M M _]]]
    ["k" [[_ _ _ _ _ _]
          [M M _ _ _ _]
          [M M _ _ _ _]
          [M M _ _ M M]
          [M M _ M M _]
          [M M M M _ _]
          [M M _ _ _ _]
          [M M M M _ _]
          [M M _ M M _]
          [M M _ _ M M]]]
    ["l" [[_ _ _] [M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M M] [_ M M]]]
    ["m" [[_ _ _ _ _ _ _ _ _ _]
          [_ _ _ _ _ _ _ _ _ _]
          [_ _ _ _ _ _ _ _ _ _]
          [M M M M M _ M M M _]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]]]
    ["n" [[_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [M M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]]]
    ["o" [[_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M _]]]
    ["p" [[_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [M M M M M _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M M M M _]
          [M M _ _ _ _]
          [M M _ _ _ _]]]
    ["q" [[_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ M M M M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M M]
          [_ _ _ _ M M]
          [_ _ _ _ M M]]]
    ["r" [[_ _ _ _ _] [] [] [M M _ M M] [M M M M M] [M M] [M M] [M M] [M M] [M M]]]
    ["s" [[_ _ _ _ _]
          [_ _ _ _ _]
          [_ _ _ _ _]
          [_ M M M _]
          [M M _ _ M]
          [M M M _ _]
          [_ M M M _]
          [_ _ M M M]
          [M _ _ M M]
          [_ M M M _]]]
    ["t" [[_ _ _ _] [_ M M] [_ M M] [M M M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ _ M M]]]
    ["u" [[_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M M]]]
    ["v" [[_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M _]
          [M M M M _ _]]]
    ["w" [[_ _ _ _ _ _ _ _ _ _]
          [_ _ _ _ _ _ _ _ _ _]
          [_ _ _ _ _ _ _ _ _ _]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M M]
          [M M _ _ M M _ _ M _]
          [M M M M M M M M _ _]]]
    ["x" [[_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ _ M M _ _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]]]
    ["y" [[_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [_ _ _ _ _ _]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [M M _ _ M M]
          [_ M M M M M]
          [_ _ _ _ M M]
          [M _ _ _ M M]
          [_ M M M M _]]]
    ["z" [[_ _ _ _ _ _] [] [] [M M M M M M] [_ _ _ _ M M] [_ _ _ M M] [_ _ M M] [_ M M] [M M] [M M M M M M]]]
    ["{" [[_ _ M M] [_ M M] [_ M M] [_ M M] [_ M M] [M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ _ M M]]]
    ["|" [[M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M] [M M]]]
    ["}" [[M M] [_ M M] [_ M M] [_ M M] [_ M M] [_ _ M M] [_ M M] [_ M M] [_ M M] [_ M M] [M M]]]
    ["~" [[_ _ _ _ _ _] [] [] [] [] [_ M M _ _ M] [M _ _ M M _]]]
  ]
}
kerning = [
  ["a" "j" 3]
  ["b" "j" 3]
  ["c" "j" 3]
  ["d" "j" 3]
  ["e" "j" 3]
  ["f" "a" 1] ["f" "c" 1] ["f" "d" 1] ["f" "e" 1] ["f" "f" 1] ["f" "g" 1] ["f" "i" 1] ["f" "j" 3] ["f" "m" 1] ["f" "n" 1]
  ["f" "o" 1] ["f" "p" 1] ["f" "q" 1] ["f" "r" 1] ["f" "s" 1] ["f" "t" 1] ["f" "u" 1] ["f" "v" 1] ["f" "w" 1] ["f" "x" 1]
  ["f" "y" 1] ["f" "z" 1] ["f" "_" 2] ["f" "." 2]
  ["h" "j" 3]
  ["i" "j" 3]
  ["k" "j" 3]
  ["l" "f" 1] ["l" "j" 3] ["l" "t" 1]
  ["m" "j" 3]
  ["n" "j" 3]
  ["o" "j" 3]
  ["p" "j" 3]
  ["r" "j" 3]
  ["s" "j" 3]
  ["t" "j" 3] ["t" "t" 1]
  ["u" "j" 3]
  ["v" "j" 3]
  ["w" "j" 3]
  ["x" "j" 3]
  ["z" "j" 3]
  ["a" "T" 2]
  ["b" "T" 2]
  ["c" "T" 2]
  ["e" "T" 2]
  ["g" "T" 2]
  ["h" "T" 2]
  ["k" "T" 2]
  ["l" "T" 1]
  ["m" "T" 2]
  ["n" "T" 2]
  ["o" "T" 2]
  ["p" "T" 2]
  ["q" "T" 2]
  ["r" "T" 2] ["r" "_" 3] ["r" "." 2]
  ["s" "T" 2]
  ["t" "T" 1]
  ["u" "T" 2]
  ["v" "T" 2] ["v" "_" 1]
  ["w" "T" 2] ["w" "_" 1]
  ["x" "T" 2]
  ["y" "T" 2]
  ["z" "T" 2]
  ["A" "j" 3]
  ["B" "j" 3]
  ["C" "j" 3]
  ["D" "j" 3]
  ["E" "f" 1] ["E" "j" 3]
  ["F" "a" 1] ["F" "c" 1] ["F" "d" 1] ["F" "e" 1] ["F" "f" 1] ["F" "g" 1] ["F" "j" 3] ["F" "m" 1] ["F" "n" 1] ["F" "o" 1]
  ["F" "p" 1] ["F" "q" 1] ["F" "r" 1] ["F" "s" 1] ["F" "t" 1] ["F" "u" 1] ["F" "v" 1] ["F" "w" 1] ["F" "x" 1]
  ["F" "y" 1] ["F" "z" 1] ["F" "J" 1] ["F" "_" 3] ["F" "." 1]
  ["G" "j" 3]
  ["H" "j" 3]
  ["I" "j" 3]
  ["J" "j" 3]
  ["K" "f" 1] ["K" "j" 3]
  ["L" "f" 1] ["L" "j" 3] ["L" "t" 1] ["L" "T" 2] ["L" "Y" 2]
  ["M" "j" 3]
  ["N" "j" 3]
  ["O" "j" 3]
  ["P" "j" 3] ["P" "J" 1] ["P" "_" 4] ["P" "." 1]
  ["Q" "a" 2] ["Q" "b" 2] ["Q" "c" 2] ["Q" "d" 2] ["Q" "e" 2] ["Q" "f" 2] ["Q" "h" 2] ["Q" "i" 2] ["Q" "k" 2]
  ["Q" "l" 2] ["Q" "m" 2] ["Q" "n" 2] ["Q" "o" 2] ["Q" "q" 2] ["Q" "r" 2] ["Q" "s" 2] ["Q" "t" 2] ["Q" "u" 2]
  ["Q" "v" 2] ["Q" "w" 2] ["Q" "x" 2] ["Q" "z" 2] ["Q" "A" 2] ["Q" "B" 2] ["Q" "C" 2] ["Q" "D" 2] ["Q" "E" 2]
  ["Q" "F" 2] ["Q" "G" 2] ["Q" "H" 2] ["Q" "I" 2] ["Q" "J" 2] ["Q" "K" 2] ["Q" "L" 2] ["Q" "M" 2] ["Q" "N" 2]
  ["Q" "O" 2] ["Q" "P" 2] ["Q" "Q" 2] ["Q" "R" 2] ["Q" "S" 2] ["Q" "T" 2] ["Q" "U" 2] ["Q" "V" 2] ["Q" "W" 2]
  ["Q" "X" 2] ["Q" "Y" 2] ["Q" "Z" 2]
  ["R" "j" 3]
  ["S" "j" 3]
  ["T" "a" 2] ["T" "c" 2] ["T" "d" 2] ["T" "e" 2] ["T" "f" 1] ["T" "g" 2] ["T" "j" 3] ["T" "m" 2] ["T" "n" 2]
  ["T" "o" 2] ["T" "p" 2] ["T" "q" 2] ["T" "r" 2] ["T" "s" 2] ["T" "t" 1] ["T" "u" 2] ["T" "v" 2] ["T" "w" 2]
  ["T" "x" 2] ["T" "y" 2] ["T" "z" 2] ["T" "J" 2]
  ["U" "j" 3]
  ["V" "j" 3] ["V" "J" 1] ["V" "_" 2] ["V" "." 1]
  ["W" "j" 3]
  ["X" "j" 3]
  ["Y" "j" 3]
  ["Z" "j" 3]
  ["_" "f" 1] ["_" "j" 3] ["_" "t" 1] ["_" "V" 2]
  ["." "f" 1] ["." "j" 2] ["." "t" 1] ["." "V" 1]
]
tofu = [[1 0 1 0 1] [0 1 0 1 0] [1 0 1 0 1] [0 1 0 1 0] [1 0 1 0 1] [0 1 0 1 0] [1 0 1 0 1] [0 1 0 1 0] [1 0 1 0 1]]

render_glyph_row_rec = |rec, row, x, y, scale, i, color| {
  if i.==(@num_words(row)) then
    nil
  else if row:i then
    draw_both(
      fill_rectangle([x.+(*(i, scale)) y], [scale scale], color),
      rec(rec, row, x, y, scale, i.+(1), color),
    )
  else
    rec(rec, row, x, y, scale, i.+(1), color)
}
render_glyph_row = |row, x, y, scale, color| {
  render_glyph_row_rec(render_glyph_row_rec, row, x, y, scale, 0, color)
}

render_glyph_rec = |rec, glyph, x, y, scale, i, color| {
  if i.==(@num_words(glyph)) then
    nil
  else
    draw_both(
      render_glyph_row(glyph:i, x, y.+(i.*(scale)), scale, color),
      rec(rec, glyph, x, y, scale, i.+(1), color),
    )
}
render_glyph = |glyph, x, y, scale, color| render_glyph_rec(render_glyph_rec, glyph, x, y, scale, 0, color)

get_glyph_rec = |rec, char, i| {
  if i.==(@num_words(font)) then tofu else if char.==(font:i:0) then font:i:1 else rec(rec, char, i.+(1))
}
get_glyph = |char| get_glyph_rec(get_glyph_rec, char, 0)

find_kerning_rec = |rec, a, b, i| {
  if i.==(@num_words(kerning)) then
    0
  else if a.==(kerning:i:0).and(b.==(kerning:i:1)) then
    kerning:i:2
  else
    rec(rec, a, b, i.+(1))
}
find_kerning = |a, b| find_kerning_rec(find_kerning_rec, a, b, 0)

# Turns a text into an object containing:
# - a linked list of [x y char glyph scale cursor_after_x cursor_after_y] values.
#   The cursor_after coordinates are the position of a cursor placed after the character.
# - the base_x
# - the base_y
# - the scale
# - the limit
layout_text_rec = |rec, text, base_x, base_y, x, y, scale| {
  if is_nil(text) then
    nil
  else {
    char = text:0
    rest = text:1
    glyph = get_glyph(char)
    pos_after = if char.==(10) then { # newline
      new_x = base_x
      new_y = y.+(14.*(scale))
      [new_x new_y new_x new_y]
    } else {
      width = @num_words(glyph:0)
      kern = if rest.is_nil() then 0 else find_kerning(char, rest.head())
      new_x = x.+(width.+(1).-(kern).*(scale))
      new_y = y
      new_cursor_x = x.+(width.-(kern./(2)).*(scale)) # cursor uses half the kerning
      [new_x new_y new_cursor_x new_y]
    }
    cons(
      [x y char glyph scale pos_after:2 pos_after:3],
      rec(rec, rest, base_x, base_y, pos_after:0, pos_after:1, scale),
    )
  }
}
layout_text = |text, x, y, scale, width| {
  [layout_text_rec(layout_text_rec, text, x, y, x, y, scale) x y scale width]
}

render_layouted_text_rec = |rec, layouted_glyphs, color| {
  if layouted_glyphs.is_nil() then
    nil
  else {
    layouted_glyph = layouted_glyphs:0
    rest = layouted_glyphs:1
    x = layouted_glyph:0
    y = layouted_glyph:1
    glyph = layouted_glyph:3
    scale = layouted_glyph:4
    draw_both(render_glyph(glyph, x, y, scale, color), rec(rec, rest, color))
  }
}
render_layouted_text = |layouted_text, color| {
  render_layouted_text_rec(render_layouted_text_rec, layouted_text:0, color)
}
render_text = |text, x, y, scale, width, color| render_layouted_text(layout_text(text, x, y, scale, width), color)

# The state contains:
# - the text as a linked list of characters
# - the cursor, which again contains some state
#  - the anchor position (where a selection started)
#  - the active position (where the part controlled by the arrow keys is)
#  - the desired x position
initial_state = [nil [0 0 0]]

start_of_cursor = |cursor| min(cursor:0, cursor:1)
end_of_cursor = |cursor| max(cursor:0, cursor:1)
has_selection = |cursor| cursor:0.!=(cursor:1)
selection = |cursor| [start_of_cursor(cursor) end_of_cursor(cursor)]

x_of_offset_in_text = |text, offset, width| {
  if offset.==(0) then 0 else layout_text(text, 0, 0, 1, width):0.get(offset.-(1)):5
}

insert_char = |state, char| {
  text = state:0
  cursor = state:1
  new_text = text.replace_range(cursor.selection(), char)
  new_offset = start_of_cursor(cursor).+(1)
  [
    new_text
    [new_offset new_offset x_of_offset_in_text(new_text, new_offset, 100)]
  ]
}
enter = |state| state.insert_char(10)
backspace = |state| {
  text = state:0
  cursor = state:1
  if cursor.has_selection() then {
    offset = start_of_cursor(cursor)
    [text.remove_range(cursor.selection()) [offset offset x_of_offset_in_text(text, offset, 100)]]
  } else {
    offset = cursor:0
    if offset.==(0) then
      state
    else
      [text.remove(offset.-(1)) [offset.-(1) offset.-(1) x_of_offset_in_text(text, offset.-(1), 100)]]
  }
}
control_backspace = |state| { state.backspace() }
delete = |state| {
  text = state:0
  cursor = state:1
  if cursor.has_selection() then {
    offset = start_of_cursor(cursor)
    [text.remove_range(cursor.selection()) [offset offset x_of_offset_in_text(text, offset, 100)]]
  } else {
    offset = cursor:0
    if offset.==(text.len()) then state else [text.remove(offset) cursor]
  }
}

find_offset_above_rec = |rec, i, x, y, glyphs| {
  # returns [offset x y] or nil
  if glyphs.is_nil() then nil else {
    glyph = glyphs:0
    our_x = glyph:5
    our_y = glyph:6
    if our_y.>=(y) then nil else {
      best_of_rest = rec(rec, i.+(1), x, y, glyphs:1)
      if best_of_rest.is_nil() then [i our_x our_y] else {
        if best_of_rest:2.>(our_y) then best_of_rest else {
          our_distance = abs(our_x.-(x))
          other_distance = abs(best_of_rest:1.-(x))
          if our_distance.<(other_distance) then [i our_x our_y] else best_of_rest
        }
      }
    }
  }
}
find_offset_above = |x, y, glyphs| {
  result = find_offset_above_rec(find_offset_above_rec, 1, x, y, glyphs)
  if result.is_nil() then nil else result:0
}
find_offset_below_rec = |rec, i, x, y, glyphs| {
  # returns [offset x y] or nil
  if glyphs.is_nil() then nil else {
    glyph = glyphs:0
    our_x = glyph:5
    our_y = glyph:6
    best_of_rest = rec(rec, i.+(1), x, y, glyphs:1)
    if our_y.<=(y) then best_of_rest else {
      if best_of_rest.is_nil() then [i our_x our_y] else {
        if our_y.<(best_of_rest:2) then [i our_x our_y] else {
          our_distance = abs(our_x.-(x))
          other_distance = abs(best_of_rest:1.-(x))
          if our_distance.<(other_distance) then [i our_x our_y] else best_of_rest
        }
      }
    }
  }
}
find_offset_below = |x, y, glyphs| {
  result = find_offset_below_rec(find_offset_below_rec, 1, x, y, glyphs)
  if result.is_nil() then nil else result:0
}

# Move one word =
# 1. Skip spaces
# 2. How many characters of punctuation follow?
#    0: Skip all letters
#    1: Skip the punctuation, then all letters after it
#    2: Skip all punctuation
is_word_char = |char| {
  char.==("_")
    .or(char.>=("a").and(char.<=("z")))
    .or(char.>=("A").and(char.<=("Z")))
    .or(char.>=("0").and(char.<=("9")))
}
is_punctuation = |char| char.!=(" ").and(not(char.is_word_char()))
num_word_chars_rec = |rec, text| {
  if text.is_nil() then 0 else if text:0.is_word_char() then 1.+(rec(rec, text:1)) else 0
}
num_word_chars = |text| num_word_chars_rec(num_word_chars_rec, text)
num_punctuation_rec = |rec, text| {
  if text.is_nil() then 0 else if text:0.is_punctuation() then 1.+(rec(rec, text:1)) else 0
}
num_punctuation = |text| num_punctuation_rec(num_punctuation_rec, text)
find_word_boundary_after_rec = |rec, text| {
  if text.is_nil() then 0 else {
    char = text:0
    if char.==(" ") then rec(rec, text:1).+(1) else {
      if char.is_word_char() then text.num_word_chars() else {
        if text:1.is_nil() then 1
        else if text:1:0.is_punctuation() then text.num_punctuation()
        else text:1.num_word_chars().+(1)
      }
    }
  }
}
find_word_boundary_after = |offset, text| {
  num_advance = find_word_boundary_after_rec(find_word_boundary_after_rec, text.skip(offset))
  offset.+(num_advance)
}
take_and_reverse_rec = |rec, list, n, so_far| {
  if list.is_nil().or(n.==(0)) then so_far else rec(rec, list:1, n.-(1), cons(list:0, so_far))
}
take_and_reverse = |list, n| take_and_reverse_rec(take_and_reverse_rec, list, n, nil)
find_word_boundary_before = |offset, text| {
  before_cursor_reversed = text.take_and_reverse(offset)
  num_advance = find_word_boundary_after_rec(find_word_boundary_after_rec, before_cursor_reversed)
  offset.-(num_advance)
}

move_left = |state, width| {
  text = state:0
  cursor = state:1
  new_offset = if cursor.has_selection() then start_of_cursor(cursor) else max(0, cursor:0.-(1))
  [text [new_offset new_offset x_of_offset_in_text(text, new_offset, width)]]
}
move_right = |state, width| {
  text = state:0
  cursor = state:1
  new_offset = if cursor.has_selection() then end_of_cursor(cursor) else min(text.len(), cursor:0.+(1))
  [text [new_offset new_offset x_of_offset_in_text(text, new_offset, width)]]
}
move_up = |state, width| {
  text = state:0
  cursor = state:1
  layouted_text = layout_text(text, 0, 0, 1, width)
  cursor_desired_x = cursor:2
  cursor_y = if cursor:1.==(0) then 0 else layouted_text:0.get(cursor:1.-(1)):6
  new_offset = find_offset_above(cursor_desired_x, cursor_y, layouted_text:0)
  new_offset = if new_offset.is_nil() then 0 else new_offset
  [text [new_offset new_offset cursor_desired_x]]
}
move_down = |state, width| {
  text = state:0
  cursor = state:1
  layouted_text = layout_text(text, 0, 0, 1, width)
  cursor_desired_x = cursor:2
  cursor_y = if cursor:1.==(0) then 0 else layouted_text:0.get(cursor:1.-(1)):6
  new_offset = find_offset_below(cursor_desired_x, cursor_y, layouted_text:0)
  new_offset = if new_offset.is_nil() then text.len() else new_offset
  [text [new_offset new_offset cursor_desired_x]]
}
move_start = |state, width| {
  text = state:0
  cursor = state:1
  find_line_start = |rec, candidate| {
    if candidate.==(0) then 0
    else if text.get(candidate.-(1)).==(10) then candidate
    else rec(rec, candidate.-(1))
  }
  line_start = find_line_start(find_line_start, cursor:1)
  [text [line_start line_start x_of_offset_in_text(text, line_start, width)]]
}
move_end = |state, width| {
  text = state:0
  cursor = state:1
  len = text.len()
  find_line_end = |rec, candidate| {
    if candidate.==(len) then len
    else if text.get(candidate).==(10) then candidate
    else rec(rec, candidate.+(1))
  }
  line_end = find_line_end(find_line_end, cursor:1)
  [text [line_end line_end x_of_offset_in_text(text, line_end, width)]]
}
shift_move_left = |state, width| {
  text = state:0
  cursor = state:1
  offset = cursor:1
  if offset.==(0) then state else [text [cursor:0 offset.-(1) x_of_offset_in_text(text, offset.-(1), width)]]
}
shift_move_right = |state, width| {
  text = state:0
  cursor = state:1
  offset = cursor:1
  if offset.==(text.len()) then
    state
  else
    [text [cursor:0 offset.+(1) x_of_offset_in_text(text, offset.+(1), width)]]
}
shift_move_up = |state, width| {
  text = state:0
  cursor = state:1
  layouted_text = layout_text(text, 0, 0, 1, width)
  cursor_desired_x = cursor:2
  cursor_y = if cursor:1.==(0) then 0 else layouted_text:0.get(cursor:1.-(1)):6
  new_offset = find_offset_above(cursor_desired_x, cursor_y, layouted_text:0)
  new_offset = if new_offset.is_nil() then 0 else new_offset
  [text [cursor:0 new_offset cursor_desired_x]]
}
shift_move_down = |state, width| {
  text = state:0
  cursor = state:1
  layouted_text = layout_text(text, 0, 0, 1, width)
  cursor_desired_x = cursor:2
  cursor_y = if cursor:1.==(0) then 0 else layouted_text:0.get(cursor:1.-(1)):6
  new_offset = find_offset_below(cursor_desired_x, cursor_y, layouted_text:0)
  new_offset = if new_offset.is_nil() then text.len() else new_offset
  [text [cursor:0 new_offset cursor_desired_x]]
}
shift_move_start = |state, width| { state.move_start(width) }
shift_move_end = |state, width| { state.move_start(width) }
ctrl_move_left = |state, width| {
  text = state:0
  cursor = state:1
  new_offset = find_word_boundary_before(cursor:1, text)
  [text [new_offset new_offset x_of_offset_in_text(text, new_offset, width)]]
}
ctrl_move_right = |state, width| {
  text = state:0
  cursor = state:1
  new_offset = find_word_boundary_after(cursor:1, text)
  [text [new_offset new_offset x_of_offset_in_text(text, new_offset, width)]]
}
ctrl_shift_move_left = |state, width| {
  text = state:0
  cursor = state:1
  new_offset = find_word_boundary_before(cursor:1, text)
  [text [cursor:0 new_offset x_of_offset_in_text(text, new_offset, width)]]
}
ctrl_shift_move_right = |state, width| {
  text = state:0
  cursor = state:1
  new_offset = find_word_boundary_after(cursor:1, text)
  [text [cursor:0 new_offset x_of_offset_in_text(text, new_offset, width)]]
}

text_width = 400

update = |state, event| {
  event_kind = event:0
  if event_kind.==("char") then {
    char = event:1
    if char.==(10).or(char.>=(32).and(char.<=(126))) then state.insert_char(char) else state
  } else if event_kind.==("key") then {
    key = event:1
    control_pressed = event:2
    shift_pressed = event:3
    if key.==(257) then state.enter()
    else if key.==(259) then { if control_pressed then state.control_backspace() else state.backspace() }
    else if key.==(261) then state.delete()
    else if key.==(262) then {
      if control_pressed then {
        if shift_pressed then state.ctrl_shift_move_right(text_width) else state.ctrl_move_right(text_width)
      } else {
        if shift_pressed then state.shift_move_right(text_width) else state.move_right(text_width)
      }
    }
    else if key.==(263) then {
      if control_pressed then {
        if shift_pressed then state.ctrl_shift_move_left(text_width) else state.ctrl_move_left(text_width)
      } else {
        if shift_pressed then state.shift_move_left(text_width) else state.move_left(text_width)
      }
    }
    else if key.==(264) then {
      if shift_pressed then state.shift_move_down(text_width) else  state.move_down(text_width)
    }
    else if key.==(265) then {
      if shift_pressed then state.shift_move_up(text_width) else  state.move_up(text_width)
    }
    else if key.==(268) then state.move_start(text_width)
    else if key.==(269) then state.move_end(text_width)
    else state
  } else crash("unknown event")
}

render_cursor = |cursor, layouted_text, color| {
  layouted_glyphs = layouted_text:0
  base_x = layouted_text:1
  base_y = layouted_text:2
  scale = layouted_text:3
  width = layouted_text:4
  if cursor.has_selection() then {
    start = start_of_cursor(cursor)
    end = end_of_cursor(cursor)
    start_position =
      if start.==(0) then [base_x base_y] else { layout = layouted_glyphs.get(start.-(1)) [layout:5 layout:6] }
    end_position = { layout = layouted_glyphs.get(end.-(1)) [layout:5 layout:6] }
    if start_position:1.==(end_position:1) then
      # The selection is on a single line.
      fill_rectangle([start_position:0 start_position:1], [end_position:0.-(start_position:0) 13.*(scale)], color)
    else {
      # The selection is on multiple lines.
      draw_both(
        fill_rectangle([start_position:0 start_position:1], [width.-(start_position:0.-(base_x)) 13.*(scale)], color),
        draw_both(
          fill_rectangle(
            [base_x start_position:1.+(13.*(scale))],
            [width end_position:1.-(start_position:1).-(14.*(scale))],
            color,
          ),
          fill_rectangle([base_x end_position:1.-(1)], [end_position:0.-(base_x) 14.*(scale)], color),
        ),
      )
    }
    # render_cursor(position:0, position:1, scale, color)
  } else {
    offset = cursor:0
    position =
      if offset.==(0) then
        [base_x.-(scale) base_y]
      else {
        layout = layouted_glyphs.get(offset.-(1))
        [layout:5 layout:6]
      }
    fill_rectangle([position:0 position:1.-(scale)], [2.*(scale) 13.*(scale)], color)
  }
}
render = |state, width, height| {
  text = state:0
  cursor = state:1
  base_x = 100
  base_y = 10
  text_scale = 1
  layouted_text = layout_text(text, base_x, base_y, text_scale, text_width)
  #draw_both(
  #  fill_path(start_path([20 200]).line_to([300 30]).line_to([400 400]), yellow),
    draw_both(
      draw_both(render_cursor(cursor, layouted_text, red), render_layouted_text(layouted_text, black)),
      draw_both(
        draw_both(
          render_text(cursor:0.int_to_str(), 10, 60, 1, 1000, red),
          render_text(cursor:1.int_to_str(), 10, 75, 1, 1000, red),
        ),
        render_text(cursor:2.int_to_str(), 10, 90, 1, 1000, red),
      ),
  #  ),
  )
}

update_rec = |update_rec, state, event| {
  new_state = update(state, event)
  [
    |width, height| render(new_state, width, height)
    |event| update_rec(update_rec, new_state, event)
    new_state
  ]
}
app = [
  |width, height| render(initial_state, width, height)
  |event| update_rec(update_rec, initial_state, event)
  initial_state
]
app
