export
  any t:Type -> t

import
  "..array"
  "..bool"
  "..byte"
  "..dynamic"
  "..int"
  "..iterator"
  "..list"
  "..maybe"
  "..never"
  "..string"
  "..type"

any t: Type -> t =
  any (type_info t) .to_static t

any type: TypeInfo -> Dynamic =
  type
  % byte -> | byte: (0.lower_byte)
    int -> | int: 0
    type -> | type
    box: inner -> | box: (any inner)
    array -> | array: (empty_array Dynamic)
    never -> crash "can't generate Never instance"
    struct: fields ->
      | struct:
          generate_array
            fields.length
            \ index: Int ->
              (& name type) = fields .get index
              (& name value: (any type))
    enum: variants ->
      (& name type) =
        variants
        . iterate
        . filter
            \ variant: (& name: String type: TypeInfo) -> variant.type.is_finite
        . first
        . unwrap
      | enum: & name value: (any type)
    lambda: (& arguments return_type) ->
      | lambda: \ args: (Array Dynamic) -> any return_type
    recursive -> crash "type is infinitely recursive"

is_finite type: TypeInfo -> Bool =
  type
  % byte  -> | true
    int   -> | true
    type  -> | true
    box: inner -> inner.is_finite
    array -> | true
    never -> | false
    struct: fields ->
      fields
      . iterate
      . all \ field: (& name: String type: TypeInfo) -> field.type.is_finite
    enum: variants ->
      variants
      . iterate
      . any
          \ variant: (& name: String type: TypeInfo) -> variant.type.is_finite
    lambda -> | true
    recursive -> | false
