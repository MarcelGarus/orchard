## Comparisons
#
# You should implement the spaceship operator <=>(T, T): Ordering for types that
# can be compared. This allows you to use operators such as <= and functions
# such as min.

import core.mar

enum Ordering { equal, greater, less }

fun ==(a: Ordering, b: Ordering): Bool {
  switch a
  case less b is less
  case equal b is equal
  case greater b is greater
}
fun hash(ord: Ordering): Int {
  switch ord case less 0 case equal 1 case greater 2
}
fun write[W](writer: W, ord: Ordering) {
  writer.write(
    switch ord case less "less" case equal "equal" case greater "greater"
  )
}
fun flip(ord: Ordering): Ordering {
  switch ord
  case less Ordering.greater
  case greater Ordering.less
  case equal Ordering.equal
}

fun <[T](a: T, b: T): Bool { a <=> b is less }
fun >[T](a: T, b: T): Bool { a <=> b is greater }
fun >=[T](a: T, b: T): Bool { not(a <=> b is less) }
fun <=[T](a: T, b: T): Bool { not(a <=> b is greater) }

fun min[T](a: T, b: T): T { if a < b then a else b }
fun max[T](a: T, b: T): T { if a > b then a else b }
fun clamp[T](a: T, range: Range[T]): T { max(range.start, min(range.end.dec(), a)) }
fun clamp[T](a: T, range: OpenRange[T]): T { max(range.start, a) }
